!>
!!               The module <i>mo_model_import_domain</i>.
!!
!!               The module <i>mo_model_import_domain</i>
!! provides functionality to import information about the models computational
!! domain. This information is read from several files that were generated by
!! the patch generator programm. The data types describing the model domain are
!! contained in <i>mo_domain_model</i>.
!!
!! @par Revision History
!! Initial version  by: Peter Korn,  MPI-M, Hamburg, June 2005
!! Modification by Thomas Heinze (2006-02-21):
!! - renamed m_modules to mo_modules
!! Modification by Thomas Heinze (2006-09-20):
!! - added routine grid_and_patch_diagnosis
!! Modification by Pilar Ripodas, DWD, (2007-01-31)
!! - addapted to the new TYPE grid_edges (system_orientation added)
!! Modification by Peter Korn,  MPI-M, (2006-12)
!! - implementation of topography and boundary treatment, i.e.
!!   initialization of the grid & patch components that carry
!!   information about topography and the lateral boundaries of
!!   the domain; this is not related to patch boundaries.
!!   topography can either be computed by analytical l,eans or
!!   by reading from database files.
!! Modification by Hui Wan, MPI-M, (2007-02-23)
!! - Subroutine <i>init_import</i> was changed to <i>setup_grid</i>.
!!   Namelist hierarchy_ini was renamed to <i>grid_ctl</i>, moved
!!   from <i>mo_io_utilities</i> to this module and now read from
!!   an external file in subroutine <i>setup_grid</i>.
!! - Some changes in <i>init_ocean_patch_component</i> after
!!   discussion with Peter.
!! - Calculation of the min. primal edge length was added to
!!   <i>import_patches</i>. However, shouldn't it be an array with
!!   one element for each patch, rather than a scalar?
!! Modification by P. Ripodas, DWD, (2007-03-14):
!! - Now the output of "import_patches" is the min_dual_edge_lenght
!!   instead of the min_primal_edge_lenght. It will be used to set
!!   the horizontal diffusion parameter. Now it is done as it was
!!   in the prototype.
!! Modification by Almut Gassmann, MPI-M (2007-04)
!! - removed loptimize to make compatible with new grid generator
!! - removed itoa for good programming style
!! - reorganized patch input to be compatible with the new patch generator
!! - cleaning up "destruct_patches"
!! Modification by Almut Gassmann, MPI-M (2007-04-13)
!! - remove grid type and perform related adaptations
!!   (grid information comes now inside a patch)
!! - changed subroutine name form setup_grid to setup_files
!! Modified by Hui Wan, MPI-M, (2008-04-04)
!!  - topography_file_dir renamed topo_file_dir
!!  - for the hydro_atmos, control variable testtype renamed ctest_name.
!! Modified by Almut Gassmann, MPI-M, (2008-04-23)
!!  - itopo distinguishes now shallow water (itopo=1) orography function
!!    from hydro_atmos orography function (itopo=2)
!! Modification by Jochen Foerstner, DWD, (2008-07-16)
!!  - new fields in the derived type for the edges:
!!    grid_edges%primal_cart_normal (Cartesian normal to edge),
!!    grid_edges%quad_idx, grid_edges%quad_area and grid_edges%quad_orientation
!!    (indices of edges and area of the quadrilateral formed by two adjacent cells)
!!    up to now these new fields are initialized in the new routines
!!    calculate_primal_cart_normal and init_quad_twoadjcells
!!    rather than read from a grid/patch file.
!! Modification by Almut Gassmann, MPI-M, (2008-09-21)
!!  - remove reference to mask and height files, they are never used
!!  - use cell_type to distinguish cells as triangles or hexagons
!! Modification by Almut Gassmann, MPI-M (2008-10-30)
!!  - add subroutine init_coriolis to initialize Coriolis parameter
!! Modification by Constantin Junk, MPI-M (2011-04-05)
!! - ...
!! Modification by Daniel Reinert, DWD (2012-04-11)
!!  - new routine which initializes the butterfly data structure
!!
!! @par Copyright
!! 2002-2007 by DWD and MPI-M
!! This software is provided for non-commercial use only.
!! See the LICENSE and the WARRANTY conditions.
!!
!! @par License
!! The use of ICON is hereby granted free of charge for an unlimited time,
!! provided the following rules are accepted and applied:
!! <ol>
!! <li> You may use or modify this code for your own non commercial and non
!!    violent purposes.
!! <li> The code may not be re-distributed without the consent of the authors.
!! <li> The copyright notice and statement of authorship must appear in all
!!    copies.
!! <li> You accept the warranty conditions (see WARRANTY).
!! <li> In case you intend to use the code commercially, we oblige you to sign
!!    an according license agreement with DWD and MPI-M.
!! </ol>
!!
!! @par Warranty
!! This code has been tested up to a certain level. Defects and weaknesses,
!! which may be included in the code, do not establish any warranties by the
!! authors.
!! The authors do not make any warranty, express or implied, or assume any
!! liability or responsibility for the use, acquisition or application of this
!! software.
!!
!!

!----------------------------
#include "omp_definitions.inc"
!----------------------------

MODULE mo_model_domimp_setup
  !-------------------------------------------------------------------------
  USE mo_kind,               ONLY: wp
  USE mo_exception,          ONLY: finish, warning
  USE mo_model_domain,       ONLY: t_patch
!   USE mo_physical_constants, ONLY: earth_angular_velocity
  USE mo_parallel_config,    ONLY: nproma
  USE mo_loopindices,        ONLY: get_indices_e
  USE mo_grid_config,        ONLY: corio_lat, grid_angular_velocity
  USE mo_sync,               ONLY: sync_c, sync_e, sync_patch_array, sync_idx
  USE mo_grid_subset,        ONLY: fill_subset,t_subset_range, get_index_range, read_subset, write_subset
  USE mo_mpi,                ONLY: work_mpi_barrier, get_my_mpi_work_id, my_process_is_mpi_seq
  USE mo_impl_constants,     ONLY: halo_levels_ceiling
  USE mo_math_utilities,     ONLY: gvec2cvec, gc2cc
  USE mo_math_types
  
  USE mo_grid_geometry_info, ONLY: planar_torus_geometry
  
  IMPLICIT NONE
  
  PRIVATE
  
  PUBLIC :: calculate_patch_cartesian_positions
  PUBLIC :: reshape_int
  PUBLIC :: reshape_real
  PUBLIC :: init_quad_twoadjcells
  PUBLIC :: init_coriolis
  PUBLIC :: set_verts_phys_id
  PUBLIC :: init_butterfly_idx
  
  PUBLIC :: fill_grid_subsets, fill_grid_subset_names, read_grid_subsets, write_grid_subsets
  
  CHARACTER(LEN=*), PARAMETER :: version = '$Id$'
  
  
  !-------------------------------------------------------------------------
  
CONTAINS

  !-------------------------------------------------------------------------
  !>
  !! This routine calculates the edge area of the patch
  !!
  SUBROUTINE calculate_edge_area(ptr_patch)
    TYPE(t_patch), TARGET, INTENT(inout) :: ptr_patch

    INTEGER                       :: je, jb, istart_e, iend_e
    TYPE(t_subset_range), POINTER :: all_edges
    !-------------------------------------------------------------------------
    all_edges => ptr_patch%edges%all

    ! a) the control volume associated to each edge is defined as the
    ! quadrilateral whose edges are the primal edge and the associated dual edge
    !----------------------------------------------------------------------------
    ! loop over all blocks and edges

!$OMP PARALLEL DO PRIVATE(jb,je) ICON_OMP_DEFAULT_SCHEDULE
    DO jb = all_edges%start_block, all_edges%end_block
      CALL get_index_range(all_edges, jb, istart_e, iend_e)
      DO je = istart_e, iend_e
        ptr_patch%edges%area_edge(je,jb) =  &
            &    ptr_patch%edges%primal_edge_length(je,jb)  &
            &  * ptr_patch%edges%dual_edge_length(je,jb)
      END DO
    END DO
!$OMP END PARALLEL DO

  END SUBROUTINE calculate_edge_area
  !-------------------------------------------------------------------------
  
  !-------------------------------------------------------------------------
  !>
  !! This method_name calculates the cartesian positions stored in patch.
  !!
  !! These values normally are read from the grid files,
  !! this routine is used only for old grids
  !!
  !! Note: cartesian_dual_middle is used only from the ocean,
  !!   which is using only new grids, therefor it is NOT calculated here
  !!
  !! @par Revision History
  !! Initial release by Jochen Foerstner (2008-05-19)
  !! Modifiaction by A. Gassmann(2010-09-05)
  !! - added also tangential normal, and generalize to lplane
  !!
  SUBROUTINE calculate_patch_cartesian_positions ( patch )
    !
    TYPE(t_patch), TARGET, INTENT(inout) :: patch  ! patch on a specific level
    
    TYPE(t_cartesian_coordinates) :: z_vec
    REAL(wp) :: z_lon, z_lat, z_u, z_v  ! location and components of normal
    REAL(wp) :: z_norm                  ! norm of Cartesian normal
    
    INTEGER :: start_index, end_index
    INTEGER :: jb, je                   ! loop indices    
    !-----------------------------------------------------------------------
                
!$OMP PARALLEL    
    ! calculate cells cartesian positions
!$OMP DO PRIVATE(jb,je, start_index, end_index) ICON_OMP_DEFAULT_SCHEDULE
    DO jb = patch%cells%all%start_block, patch%cells%all%end_block
      CALL get_index_range(patch%cells%all, jb, start_index, end_index)
      DO je = start_index, end_index            
        ! location of cell center
        patch%cells%cartesian_center(je,jb) = gc2cc(patch%cells%center(je,jb))
      ENDDO
    ENDDO
!$OMP END DO NOWAIT
    
    ! calculate verts cartesian positions
!$OMP DO PRIVATE(jb,je, start_index, end_index) ICON_OMP_DEFAULT_SCHEDULE
    DO jb = patch%verts%all%start_block, patch%verts%all%end_block
      CALL get_index_range(patch%verts%all, jb, start_index, end_index)
      DO je = start_index, end_index            
        ! location of cell center
        patch%verts%cartesian(je,jb) = gc2cc(patch%verts%vertex(je,jb))
      ENDDO
    ENDDO
!$OMP END DO NOWAIT

    ! calculate edges positions
!$OMP DO PRIVATE(jb,je,z_lon,z_lat,z_u,z_v,z_norm,z_vec, start_index, end_index) ICON_OMP_DEFAULT_SCHEDULE
    DO jb = patch%edges%all%start_block, patch%edges%all%end_block
      CALL get_index_range(patch%edges%all, jb, start_index, end_index)
      DO je = start_index, end_index
            
        ! location of edge midpoint
        patch%edges%cartesian_center(je,jb) = gc2cc(patch%edges%center(je,jb))
        z_lon = patch%edges%center(je,jb)%lon
        z_lat = patch%edges%center(je,jb)%lat
        
        ! zonal and meridional component of primal normal
        z_u = patch%edges%primal_normal(je,jb)%v1
        z_v = patch%edges%primal_normal(je,jb)%v2
        
        ! calculate Cartesian components of primal normal
        CALL gvec2cvec( z_u, z_v, z_lon, z_lat, z_vec%x(1), z_vec%x(2), z_vec%x(3) )
        
        ! compute unit normal to edge je
        z_norm = SQRT( DOT_PRODUCT(z_vec%x(1:3),z_vec%x(1:3)) )
        z_vec%x(1:3) = 1._wp / z_norm * z_vec%x(1:3)
        
        ! save the values in the according type structure of the patch
!         WRITE(*,*) "----------------------------------"
!         write(*,*) "primal_cart_normal:", patch%edges%primal_cart_normal(je,jb)%x(:)
!         write(*,*) "z_vec:", z_vec%x
!         WRITE(*,*) "----------------------------------"
!         IF ( MAXVAL(ABS(patch%edges%primal_cart_normal(je,jb)%x - z_vec%x)) > 0.0001_wp) &
!           CALL finish("","primal_cart_normal(je,jb)%x /=  z_vec%x")
               
        patch%edges%primal_cart_normal(je,jb)%x(1) = z_vec%x(1)
        patch%edges%primal_cart_normal(je,jb)%x(2) = z_vec%x(2)
        patch%edges%primal_cart_normal(je,jb)%x(3) = z_vec%x(3)
        
        ! zonal and meridional component of dual normal
        z_u = patch%edges%dual_normal(je,jb)%v1
        z_v = patch%edges%dual_normal(je,jb)%v2
        
        ! calculate Cartesian components of primal normal
        CALL gvec2cvec( z_u, z_v, z_lon, z_lat, z_vec%x(1), z_vec%x(2), z_vec%x(3) )
        
        ! compute unit normal to edge je
        z_norm = SQRT( DOT_PRODUCT(z_vec%x(1:3),z_vec%x(1:3)) )
        z_vec%x(1:3) = 1._wp / z_norm * z_vec%x(1:3)
        
!         WRITE(*,*) "----------------------------------"
!         write(*,*) "dual_cart_normal:", patch%edges%dual_cart_normal(je,jb)%x(:)
!         write(*,*) "z_vec:", z_vec%x
!         WRITE(*,*) "----------------------------------"
!         IF ( MAXVAL(ABS(patch%edges%dual_cart_normal(je,jb)%x - z_vec%x)) > 0.0001_wp) &
!           CALL finish("","dual_cart_normal(je,jb)%x /=  z_vec%x")
        
        ! save the values in the according type structure of the patch
        patch%edges%dual_cart_normal(je,jb)%x(1) = z_vec%x(1)
        patch%edges%dual_cart_normal(je,jb)%x(2) = z_vec%x(2)
        patch%edges%dual_cart_normal(je,jb)%x(3) = z_vec%x(3)
        
      END DO
      
    END DO
!$OMP END DO NOWAIT
!$OMP END PARALLEL
    
  END SUBROUTINE calculate_patch_cartesian_positions
  !-------------------------------------------------------------------------
    
    
  !-------------------------------------------------------------------------
  !>
  !! This routine initializes the data for the quadrilateral cells.
  !!
  !! This routine initializes the data for the quadrilateral cells
  !! formed by the two adjacent cells of an edge.
  !! The four edge indices of this qudrilateral and its area are stored
  !! in the derived type for the edges.
  !! This information should be provided by the grid generator and
  !! read in the routine read_patch.
  !!
  !! @par Revision History
  !! Initial release by Jochen Foerstner (2008-07-16)
  !! Modification by Guenther Zaengl (2009-05-08): vectorization
  !! Also suitable for hexagons: Almut Gassmann (2009-10-01)
  !!
  SUBROUTINE init_quad_twoadjcells( patch )
    !
    TYPE(t_patch), TARGET, INTENT(inout) :: patch  ! patch on a specific level
    
    INTEGER :: nblks_e
    INTEGER :: je, jb                   ! loop index
    INTEGER :: iie, iie1(nproma), iie2(nproma), ierror(nproma)
    INTEGER :: ilc1, ibc1, ilc2, ibc2                   ! cell indices
    INTEGER :: ile1, ibe1, ile2, ibe2, ile3, ibe3       ! edge indices
    INTEGER :: ilv1, ibv1, ilv2, ibv2, ilev, ibev, jjev ! vertex indices
    INTEGER :: i_startblk, i_startidx, i_endidx
    
    
    !-----------------------------------------------------------------------
    
    ! values for the blocking
    nblks_e  = patch%nblks_e
    
    ! Quad cells cannot be computed along the lateral edges of a nested domain
    i_startblk = patch%edges%start_blk(2,1)
    
!$OMP PARALLEL
    
    ! Initialize array elements along nest boundaries with zero
    IF (patch%id > 1) THEN
!$OMP WORKSHARE
      patch%edges%quad_area(:,1:i_startblk)            = 0._wp
      patch%edges%quad_idx(:,1:i_startblk,1:4)         = 0
      patch%edges%quad_blk(:,1:i_startblk,1:4)         = 0
      patch%edges%quad_orientation(:,1:i_startblk,1:4) = 0._wp
!$OMP END WORKSHARE
    ENDIF
    
!$OMP DO PRIVATE(jb,i_startidx,i_endidx,je,iie,ilc1,ibc1,ilc2,ibc2,ile1,ibe1,ile2,ibe2,&
!$OMP            ile3,ibe3,iie1,iie2,ierror,ilv1,ibv1,ilv2,ibv2,ilev,ibev,&
!$OMP jjev) ICON_OMP_DEFAULT_SCHEDULE
    DO jb = i_startblk, nblks_e
      
      CALL get_indices_e(patch, jb, i_startblk, nblks_e, &
        & i_startidx, i_endidx, 2)
      
      IF (patch%cell_type == 3) THEN ! vectorized code for triangular grid
        
        ierror = 0
        
        DO je = i_startidx, i_endidx
          
          IF(.NOT.patch%edges%owner_mask(je,jb)) CYCLE
          !
          ! get global indices of the edges of the two neighboring cells
          !
          ilc1 = patch%edges%cell_idx(je,jb,1)
          ibc1 = patch%edges%cell_blk(je,jb,1)
          ilc2 = patch%edges%cell_idx(je,jb,2)
          ibc2 = patch%edges%cell_blk(je,jb,2)
          
          ! sum area of the two adjacent cells
          patch%edges%quad_area(je,jb) = &
            & patch%cells%area(ilc1,ibc1) + patch%cells%area(ilc2,ibc2)
          
          ! Indices of edges adjacent to cell 1
          ile1 = patch%cells%edge_idx(ilc1,ibc1,1)
          ibe1 = patch%cells%edge_blk(ilc1,ibc1,1)
          ile2 = patch%cells%edge_idx(ilc1,ibc1,2)
          ibe2 = patch%cells%edge_blk(ilc1,ibc1,2)
          ile3 = patch%cells%edge_idx(ilc1,ibc1,3)
          ibe3 = patch%cells%edge_blk(ilc1,ibc1,3)
          
          IF (je == ile1 .AND. jb == ibe1) THEN
            iie1(je) = 2
            iie2(je) = 3
          ELSE IF (je == ile2 .AND. jb == ibe2) THEN
            iie1(je) = 3
            iie2(je) = 1
          ELSE IF (je == ile3 .AND. jb == ibe3) THEN
            iie1(je) = 1
            iie2(je) = 2
          ELSE
            ierror(je) = ierror(je) + 1
          ENDIF
          
          patch%edges%quad_idx(je,jb,1) = patch%cells%edge_idx(ilc1,ibc1,iie1(je))
          patch%edges%quad_blk(je,jb,1) = patch%cells%edge_blk(ilc1,ibc1,iie1(je))
          patch%edges%quad_orientation(je,jb,1) =  &
            & patch%cells%edge_orientation(ilc1,ibc1,iie1(je))
          
          patch%edges%quad_idx(je,jb,2) = patch%cells%edge_idx(ilc1,ibc1,iie2(je))
          patch%edges%quad_blk(je,jb,2) = patch%cells%edge_blk(ilc1,ibc1,iie2(je))
          patch%edges%quad_orientation(je,jb,2) =  &
            & patch%cells%edge_orientation(ilc1,ibc1,iie2(je))
          
          ! Indices of edges adjacent to cell 2
          ile1 = patch%cells%edge_idx(ilc2,ibc2,1)
          ibe1 = patch%cells%edge_blk(ilc2,ibc2,1)
          ile2 = patch%cells%edge_idx(ilc2,ibc2,2)
          ibe2 = patch%cells%edge_blk(ilc2,ibc2,2)
          ile3 = patch%cells%edge_idx(ilc2,ibc2,3)
          ibe3 = patch%cells%edge_blk(ilc2,ibc2,3)
          
          IF (je == ile1 .AND. jb == ibe1) THEN
            iie1(je) = 2
            iie2(je) = 3
          ELSE IF (je == ile2 .AND. jb == ibe2) THEN
            iie1(je) = 3
            iie2(je) = 1
          ELSE IF (je == ile3 .AND. jb == ibe3) THEN
            iie1(je) = 1
            iie2(je) = 2
          ELSE
            ierror(je) = ierror(je) + 1
          ENDIF
          
          patch%edges%quad_idx(je,jb,3) = patch%cells%edge_idx(ilc2,ibc2,iie1(je))
          patch%edges%quad_blk(je,jb,3) = patch%cells%edge_blk(ilc2,ibc2,iie1(je))
          patch%edges%quad_orientation(je,jb,3) =  &
            & patch%cells%edge_orientation(ilc2,ibc2,iie1(je))
          
          patch%edges%quad_idx(je,jb,4) = patch%cells%edge_idx(ilc2,ibc2,iie2(je))
          patch%edges%quad_blk(je,jb,4) = patch%cells%edge_blk(ilc2,ibc2,iie2(je))
          patch%edges%quad_orientation(je,jb,4) =  &
            & patch%cells%edge_orientation(ilc2,ibc2,iie2(je))
          
        ENDDO
        IF (MAXVAL(ierror) > 0) THEN
          WRITE(0,*) "Number of errors", SUM(ierror(1:nproma))
          CALL finish ('mo_model_domain_import:init_quad_twoadjcells',  &
            & 'edge-cell index relationships are apparently incorrect')
        ENDIF
        
      ELSE ! cell_type == 6
        
        DO je = i_startidx, i_endidx
          
          IF(.NOT.patch%edges%owner_mask(je,jb)) CYCLE
          
          iie = 0
          !
          ! get global indices of the edges of the two neighboring verts
          !
          ilv1 = patch%edges%vertex_idx(je,jb,1)
          ibv1 = patch%edges%vertex_blk(je,jb,1)
          ilv2 = patch%edges%vertex_idx(je,jb,2)
          ibv2 = patch%edges%vertex_blk(je,jb,2)
          
          ! sum area of the two adjacent verts
          patch%edges%quad_area(je,jb) = &
            & patch%verts%dual_area(ilv1,ibv1) + patch%verts%dual_area(ilv2,ibv2)
          
          DO jjev = 1, 3
            
            ilev = patch%verts%edge_idx(ilv1,ibv1,jjev)
            ibev = patch%verts%edge_blk(ilv1,ibv1,jjev)
            
            ! test if edge is not the current one, then store line and
            ! block indices and orientation of quad edge
            IF ( ilev /= je .OR. ibev /= jb ) THEN
              iie = iie+1
              patch%edges%quad_idx(je,jb,iie) = ilev
              patch%edges%quad_blk(je,jb,iie) = ibev
              patch%edges%quad_orientation(je,jb,iie) =  &
                & patch%verts%edge_orientation(ilv1,ibv1,jjev)
            ENDIF
            
          ENDDO
          
          DO jjev = 1, 3
            
            ilev = patch%verts%edge_idx(ilv2,ibv2,jjev)
            ibev = patch%verts%edge_blk(ilv2,ibv2,jjev)
            
            ! test if edge is not the current one, then store line and
            ! block indices and orientation of quad edge
            IF ( ilev /= je .OR. ibev /= jb ) THEN
              iie = iie+1
              patch%edges%quad_idx(je,jb,iie) = ilev
              patch%edges%quad_blk(je,jb,iie) = ibev
              patch%edges%quad_orientation(je,jb,iie) =  &
                & patch%verts%edge_orientation(ilv2,ibv2,jjev)
            ENDIF
            
          ENDDO
          
          IF (iie /= 4) THEN
            PRINT *, "ERROR ==>  iie = ", iie,  " /= 4 "
            CALL finish ('mo_model_domain_import:init_quad_twoadjcells',  &
              & 'wrong number of edge indices for quad')
          ENDIF
          
        END DO ! end edge loop
        
      ENDIF ! cell_type
      
    END DO
!$OMP END DO NOWAIT
!$OMP END PARALLEL
    
    CALL sync_patch_array(sync_e,patch,patch%edges%quad_area)
    DO iie = 1, 4
      CALL sync_patch_array(sync_e,patch,patch%edges%quad_orientation(:,:,iie))
      
      CALL sync_idx(sync_e,sync_e,patch,patch%edges%quad_idx(:,:,iie), &
        & patch%edges%quad_blk(:,:,iie), &
        & opt_remap=.FALSE.)
    ENDDO
    
    
  END SUBROUTINE init_quad_twoadjcells
  !-------------------------------------------------------------------------
  

  !-------------------------------------------------------------------------
  !>
  !! This routine initializes the butterfly index field.
  !!
  !! This routine initializes the butterfly index field, formed by the 
  !! 4 cells sharing the 2 vertices which bound a given edge.
  !! The 4 cell indices are stored in the derived type for the edges.
  !!
  !!     -------------------------------
  !!       \          /\          /
  !!        \ (2,2)  /  \  (2,1) /             (edge_neighbor,cell)
  !!         \      /    \      /
  !!          \    /      \    /
  !!           \  /   2    \  /             ^  : edge normal
  !!            \/          \/             /|\
  !!       ----2-============-1---------    |
  !!            /\          /\
  !!           /  \   1    /  \
  !!          /    \      /    \
  !!         /      \    /      \
  !!        /  (1,2) \  /  (1,1) \
  !!       /          \/          \
  !!     -------------------------------
  !!
  !!
  !! Storage order:  butterfly_idx(nproma,nblks_c,edge_neighbor,cell)
  !!                 butterfly_blk(nproma,nblks_c,edge_neighbor,cell)
  !!
  !! The cells are subdivided into two classes: Cells which are 
  !! neighbors of edge-neighbor 1 and cells which are neighbors of edge-
  !! neighbor 2. These cells are then numbered according to the number 
  !! of the edge-vertex they share. (see ASCII-ART)
  !!
  !! @par Revision History
  !! Initial release by Daniel Reinert (2012-04-05)
  !!
  SUBROUTINE init_butterfly_idx( patch )
    !
    TYPE(t_patch), TARGET, INTENT(inout) :: patch  ! patch on a specific level

    INTEGER :: cnt               ! counter
    INTEGER :: nblks_e
    INTEGER :: rl_start
    INTEGER :: je, jen, jn, jv, jb                   ! loop index
    INTEGER :: iln, ibn                              ! cell indices (neighbors)
    INTEGER :: iie, ije
    INTEGER :: il_bf1(2), ib_bf1(2)
    INTEGER :: il_bf2(2), ib_bf2(2)
    INTEGER :: ilc1, ibc1, ilc2, ibc2                ! cell indices
    INTEGER :: ilv1, ibv1, ilv2, ibv2, ilv, ibv      ! vertex indices
    INTEGER :: i_startblk, i_startidx, i_endidx
    
    
    !-----------------------------------------------------------------------
    
    ! values for the blocking
    nblks_e  = patch%nblks_e
    
    rl_start = 3
    i_startblk = patch%edges%start_blk(rl_start,1)
    
!$OMP PARALLEL
    
    ! Initialize array elements along nest boundaries with zero
    IF (patch%id > 1) THEN
!$OMP WORKSHARE
      patch%edges%butterfly_idx(:,1:i_startblk,:,:)  = 0
      patch%edges%butterfly_blk(:,1:i_startblk,:,:)  = 0
!$OMP END WORKSHARE
    ENDIF
    
!$OMP DO PRIVATE(jb,je,i_startidx,i_endidx,ilc1,ilc2,ibc1,ibc2, &
!$OMP            cnt,jen,iln,ibn,il_bf1,ib_bf1,il_bf2,ib_bf2,   &
!$OMP            ilv1,ilv2,ibv1,ibv2,jn,jv,ilv,ibv)
    DO jb = i_startblk, nblks_e
      
      CALL get_indices_e(patch, jb, i_startblk, nblks_e, &
        & i_startidx, i_endidx, rl_start)
      
        
      DO je = i_startidx, i_endidx
          
        IF(.NOT.patch%edges%owner_mask(je,jb)) CYCLE

        !
        ! get global indices of the two neighboring cells
        !
        ilc1 = patch%edges%cell_idx(je,jb,1)
        ibc1 = patch%edges%cell_blk(je,jb,1)
        ilc2 = patch%edges%cell_idx(je,jb,2)
        ibc2 = patch%edges%cell_blk(je,jb,2)
          

        ! get global indices of the two cells neighboring 
        ! each edge-neighbor, while excluding the other 
        ! edge neighbor.
        !
        ! neighbor 1 (ilc1,ibc1)
        cnt = 0
        DO jen = 1,3
          iln= patch%cells%neighbor_idx(ilc1,ibc1,jen)
          ibn= patch%cells%neighbor_blk(ilc1,ibc1,jen)
          IF ((iln == ilc2) .AND. (ibn == ibc2)) CYCLE
          cnt = cnt + 1
          il_bf1(cnt) = iln
          ib_bf1(cnt) = ibn
        ENDDO


        ! neighbor 2 (ilc2,ibc2)
        cnt = 0
        DO jen = 1,3
          iln= patch%cells%neighbor_idx(ilc2,ibc2,jen)
          ibn= patch%cells%neighbor_blk(ilc2,ibc2,jen)
          IF ((iln == ilc1) .AND. (ibn == ibc1)) CYCLE
          cnt = cnt + 1
          il_bf2(cnt) = iln
          ib_bf2(cnt) = ibn
        ENDDO


        ! Now, the global indices of the 4 cells are known, and 
        ! they are already distinguished with respect to the edge 
        ! neighbor. However, we do not know yet, whether they share 
        ! vertex 1 or 2.

        ! Indices of vertices bounding edge je
        !
        ilv1= patch%edges%vertex_idx(je,jb,1)
        ibv1= patch%edges%vertex_blk(je,jb,1)
        ilv2= patch%edges%vertex_idx(je,jb,2)
        ibv2= patch%edges%vertex_blk(je,jb,2)


        ! check neighbors of neighbor 1, whether they share 
        ! vertex 1 or 2
        DO jn= 1, 2   ! loop over 2 neighbors of neighbor 1
          !
          ! get vertex indices of neighbor jn
          DO jv=1,3
            ilv = patch%cells%vertex_idx(il_bf1(jn),ib_bf1(jn),jv)
            ibv = patch%cells%vertex_blk(il_bf1(jn),ib_bf1(jn),jv)

            IF ( ilv == ilv1 .AND. ibv == ibv1 ) THEN
              patch%edges%butterfly_idx(je,jb,1,1) = il_bf1(jn)
              patch%edges%butterfly_blk(je,jb,1,1) = ib_bf1(jn)
            ELSE IF ( ilv == ilv2 .AND. ibv == ibv2 ) THEN
              patch%edges%butterfly_idx(je,jb,1,2) = il_bf1(jn)
              patch%edges%butterfly_blk(je,jb,1,2) = ib_bf1(jn)
            ENDIF
          ENDDO
        ENDDO

        ! check neighbors of neighbor 2, whether they share 
        ! vertex 1 or 2
        DO jn= 1, 2   ! loop over 2 neighbors of neighbor 2
          !
          ! get vertex indices of neighbor jn
          DO jv=1,3
            ilv = patch%cells%vertex_idx(il_bf2(jn),ib_bf2(jn),jv)
            ibv = patch%cells%vertex_blk(il_bf2(jn),ib_bf2(jn),jv)

            IF ( ilv == ilv1 .AND. ibv == ibv1 ) THEN
              patch%edges%butterfly_idx(je,jb,2,1) = il_bf2(jn)
              patch%edges%butterfly_blk(je,jb,2,1) = ib_bf2(jn)
            ELSE IF ( ilv == ilv2 .AND. ibv == ibv2 ) THEN
              patch%edges%butterfly_idx(je,jb,2,2) = il_bf2(jn)
              patch%edges%butterfly_blk(je,jb,2,2) = ib_bf2(jn)
            ENDIF
          ENDDO
        ENDDO       

      END DO  ! je      
    END DO  ! jb
!$OMP END DO
!$OMP END PARALLEL

    DO iie = 1, 2
      DO ije = 1, 2
        CALL sync_idx(sync_e,sync_c,patch,            &
          &  patch%edges%butterfly_idx(:,:,iie,ije),  &
          &  patch%edges%butterfly_blk(:,:,iie,ije),  &
          &  opt_remap=.FALSE.)
      ENDDO
    ENDDO
    
    
  END SUBROUTINE init_butterfly_idx



  !-------------------------------------------------------------------------
  !>
  !! Initializes the Coriolis components of the grid with analytical values.
  !!
  !! @par Revision History
  !! Developed by Almut Gassmann, MPI-M (2008-10-30).
  !! Modification by Daniel Reinert, DWD (2010-07-13),
  !! - Allocation of f_c, f_e, f_v has been moved to read_patch
  !!
  SUBROUTINE init_coriolis( lcorio, lplane, patch )
    
    LOGICAL,INTENT(in) :: lcorio, lplane
    TYPE(t_patch), TARGET, INTENT(inout) :: patch ! patch on specific level
    
    INTEGER :: nlen,                         &
      & nblks_c,  nblks_e,  nblks_v,  &
      & npromz_c, npromz_e, npromz_v
    INTEGER :: jc, je, jv, jb
    
    LOGICAL :: is_plane
    REAL(wp) :: zlat
    
    !-----------------------------------------------------------------------
    
    ! values for the blocking
    nblks_c  = patch%nblks_c
    npromz_c = patch%npromz_c
    nblks_e  = patch%nblks_e
    npromz_e = patch%npromz_e
    nblks_v  = patch%nblks_v
    npromz_v = patch%npromz_v

    is_plane = lplane .OR. &
      &        (patch%geometry_info%geometry_type == planar_torus_geometry)
    
    IF (lcorio .AND. .NOT. is_plane) THEN
      
      DO jb = 1, nblks_c
        IF (jb /= nblks_c) THEN
          nlen = nproma
        ELSE
          nlen = npromz_c
        ENDIF
        DO jc = 1, nlen
          zlat = patch%cells%center(jc,jb)%lat
          patch%cells%f_c(jc,jb) = 2._wp * grid_angular_velocity * SIN(zlat)
        END DO
      END DO
      
      DO jb = 1, nblks_e
        IF (jb /= nblks_e) THEN
          nlen = nproma
        ELSE
          nlen = npromz_e
        ENDIF
        DO je = 1, nlen
          zlat = patch%edges%center(je,jb)%lat
          patch%edges%f_e(je,jb) = 2._wp * grid_angular_velocity * SIN(zlat)
        END DO
      END DO
      
      DO jb = 1, nblks_v
        IF (jb /= nblks_v) THEN
          nlen = nproma
        ELSE
          nlen = npromz_v
        ENDIF
        DO jv = 1, nlen
          zlat = patch%verts%vertex(jv,jb)%lat
          patch%verts%f_v(jv,jb) = 2._wp * grid_angular_velocity * SIN(zlat)
        END DO
      END DO
      
    ELSEIF (lcorio .AND. is_plane) THEN
      
      patch%cells%f_c(:,:) = 2._wp*grid_angular_velocity*SIN(corio_lat)
      patch%edges%f_e(:,:) = 2._wp*grid_angular_velocity*SIN(corio_lat)
      patch%verts%f_v(:,:) = 2._wp*grid_angular_velocity*SIN(corio_lat)
      
    ELSE
      grid_angular_velocity = 0.0_wp
      patch%cells%f_c(:,:) = 0.0_wp
      patch%edges%f_e(:,:) = 0.0_wp
      patch%verts%f_v(:,:) = 0.0_wp
      
    ENDIF
    
  END SUBROUTINE init_coriolis
  !-------------------------------------------------------------------------
  
  !-------------------------------------------------------------------------
  !>
  !! Sets phys_id for verts since this is not read from input
  !!
  !! @par Revision History
  !! Developed by Rainer Johanni (2010-12-05).
  !!
  SUBROUTINE set_verts_phys_id( patch )
    
    TYPE(t_patch), INTENT(inout) :: patch ! patch on specific level
    
    INTEGER :: nlen, jb, jv, ji, ilc, ibc
    
    !-----------------------------------------------------------------------
    
    IF (patch%cell_type == 3) THEN
      
      DO jb = 1, patch%nblks_v
        IF (jb /= patch%nblks_v) THEN
          nlen = nproma
        ELSE
          nlen = patch%npromz_v
        ENDIF
        DO jv = 1, nlen
          ! Just go over all neighboring cells of a vertex and take the phys_id
          ! of the first valid neighbor cell
          DO ji = 1, 6
            ilc = patch%verts%cell_idx(jv,jb,ji)
            ibc = patch%verts%cell_blk(jv,jb,ji)
            IF(ilc > 0) THEN
              patch%verts%phys_id(jv,jb) = patch%cells%phys_id(ilc, ibc)
              EXIT
            ENDIF
          END DO
        END DO
      END DO
      
    ELSE
      
      ! There is no distinction between physical and logical patches for hex grids
      patch%verts%phys_id(:,:) = patch%id
      
    ENDIF
    
  END SUBROUTINE set_verts_phys_id
  !-------------------------------------------------------------------------
  
  !-------------------------------------------------------------------------
  !>
  !!               reshape an integer array for the blocking.
  !!
  !! @par Revision History
  !! Developed  by  Jochen Foerstner, DWD (2008-10-21)
  !!
  SUBROUTINE reshape_int( p_int_array_in, nblks, npromz,  &
    & p_reshaped_int_array_out )
    
    
    ! input array
    INTEGER, INTENT(in) :: p_int_array_in(:)
    ! number of blocks
    INTEGER, INTENT(in) :: nblks
    ! chunk length
    INTEGER, INTENT(in) :: npromz
    
    ! output array
    INTEGER, INTENT(inout) :: p_reshaped_int_array_out(:,:)
    
    INTEGER :: nlen
    INTEGER :: jl, jb
    INTEGER :: il
    
    !-----------------------------------------------------------------------
    
    DO jb = 1, nblks
      
      IF (jb /= nblks) THEN
        nlen = nproma
      ELSE
        nlen = npromz
        DO jl = npromz+1, nproma
          p_reshaped_int_array_out(jl,nblks) = 0
        END DO
      END IF
      
      DO jl = 1, nlen
        il = jl + (jb-1)*nproma
        p_reshaped_int_array_out(jl,jb) = p_int_array_in(il)
      END DO
      
    END DO
    
  END SUBROUTINE reshape_int
  !-------------------------------------------------------------------------
  
  !-------------------------------------------------------------------------
  !>
  !!               reshape a real array for the blocking.
  !!
  !!
  !! @par Revision History
  !! Developed  by  Jochen Foerstner, DWD (2008-10-21)
  !!
  SUBROUTINE reshape_real( p_real_array_in, nblks, npromz,  &
    & p_reshaped_real_array_out )
    
    ! input array
    REAL(wp), INTENT(in):: p_real_array_in(:)
    ! number of blocks
    INTEGER, INTENT(in) :: nblks
    ! chunk length
    INTEGER, INTENT(in) :: npromz
    
    ! output array
    REAL(wp), INTENT(inout) :: p_reshaped_real_array_out(:,:)
    
    INTEGER :: nlen
    INTEGER :: jl, jb
    INTEGER :: il
    
    !-----------------------------------------------------------------------
    
    DO jb = 1, nblks
      
      IF (jb /= nblks) THEN
        nlen = nproma
      ELSE
        nlen = npromz
        DO jl = npromz+1, nproma
          p_reshaped_real_array_out(jl,nblks) = 0._wp
        END DO
      END IF
      
      DO jl = 1, nlen
        il = jl + (jb-1)*nproma
        p_reshaped_real_array_out(jl,jb) = p_real_array_in(il)
      END DO
      
    END DO
    
  END SUBROUTINE reshape_real
  !-------------------------------------------------------------------------
  
  !-----------------------------------------------------------------------------
  !>
  !! Fills the grid's subsets
  SUBROUTINE fill_grid_subsets(patch)
    TYPE(t_patch), INTENT(inout) :: patch
    
    !--------------------------------------------------------------------------------
    ! aliasing the halo_level to decomp_domain
    patch%cells%halo_level => patch%cells%decomp_domain
    patch%edges%halo_level => patch%edges%decomp_domain
    patch%verts%halo_level => patch%verts%decomp_domain
    !--------------------------------------------------------------------------------
    ! make sure the levels are correct when running sequentially
    IF (my_process_is_mpi_seq()) THEN
      patch%cells%halo_level(:,:) = 0
      patch%cells%halo_level(patch%npromz_c + 1 :nproma, patch%nblks_c) = -1
      patch%edges%halo_level(:,:) = 0
      patch%edges%halo_level(patch%npromz_e + 1 :nproma, patch%nblks_e) = -1
      patch%verts%halo_level(:,:) = 0
      patch%verts%halo_level(patch%npromz_v + 1 :nproma, patch%nblks_v) = -1
    ENDIF

    !--------------------------------------------------------------------------------
    CALL fill_grid_subset_names(patch)
    !--------------------------------------------------------------------------------
    ! fill cell subsets
    !    fill_subset(range subset,  halo levels, start level, end level)
    CALL fill_subset(patch%cells%all, patch, patch%cells%halo_level, &
      & 0, halo_levels_ceiling)
    patch%cells%all%is_in_domain   = .false.
    
    CALL fill_subset(patch%cells%owned, patch, patch%cells%halo_level, 0, 0)
    patch%cells%owned%is_in_domain = .true.
    
    CALL fill_subset(patch%cells%in_domain, patch, patch%cells%halo_level, 0, 0)
    patch%cells%in_domain%is_in_domain = .true.
    
    CALL fill_subset(patch%cells%not_owned, patch, patch%cells%halo_level,&
      & 1, halo_levels_ceiling)
    patch%cells%not_owned%is_in_domain = .false.
    
    CALL fill_subset(patch%cells%not_in_domain,  patch, &
      & patch%cells%halo_level, 2, halo_levels_ceiling)
    patch%cells%not_in_domain%is_in_domain = .false.
    
    CALL fill_subset(patch%cells%one_edge_in_domain, patch, patch%cells%halo_level, 0, 1)
    patch%cells%one_edge_in_domain%is_in_domain = .false.
    
    IF (patch%cells%in_domain%no_of_holes > 0) &
      CALL warning("patch%cells%in_domain", "no_of_holes > 0")
    IF (patch%cells%one_edge_in_domain%no_of_holes > 0) &
      CALL warning("patch%cells%one_edge_in_domain", "no_of_holes > 0")
    
    ! fill edge subsets
    !    fill_subset(range subset,  halo levels, start level, end level)
    CALL fill_subset(patch%edges%all, patch, patch%edges%halo_level, 0, halo_levels_ceiling)
    patch%edges%all%is_in_domain   = .false.
    
    CALL fill_subset(patch%edges%owned, patch,patch%edges%halo_level, 0, 0)
    patch%edges%owned%is_in_domain = .true.
    
    CALL fill_subset(patch%edges%in_domain, patch, patch%edges%halo_level, 0, 1)
    patch%edges%in_domain%is_in_domain   = .true.
    
    CALL fill_subset(patch%edges%not_owned, patch, patch%edges%halo_level, &
      & 1, halo_levels_ceiling)
    patch%edges%not_owned%is_in_domain   = .false.
    
    CALL fill_subset(patch%edges%not_in_domain, patch, &
      & patch%edges%halo_level, 2, halo_levels_ceiling)
    patch%edges%not_in_domain%is_in_domain   = .false.
    
    ! fill vertex subsets
    !    fill_subset(range subset,  halo levels, start level, end level)
    CALL fill_subset(patch%verts%all,  patch, patch%verts%halo_level, &
      & 0, halo_levels_ceiling)
    patch%verts%all%is_in_domain   = .false.
    
    CALL fill_subset(patch%verts%owned, patch, patch%verts%halo_level, 0, 0)
    patch%verts%owned%is_in_domain   = .true.
    
    CALL fill_subset(patch%verts%in_domain, patch, patch%verts%halo_level, 0, 1)
    patch%verts%in_domain%is_in_domain   = .true.
    
    CALL fill_subset(patch%verts%not_owned, patch, patch%verts%halo_level, 1, halo_levels_ceiling)
    patch%verts%not_owned%is_in_domain   = .false.
    
    CALL fill_subset(patch%verts%not_in_domain, patch, &
      & patch%verts%halo_level, 2, halo_levels_ceiling)
    patch%verts%not_in_domain%is_in_domain   = .false.
          
    ! write some info:
    !     IF (get_my_mpi_work_id() == 1) CALL work_mpi_barrier()
    !     write(0,*) get_my_mpi_work_id(), "egdes global_index:", patch%edges%glb_index
    !     write(0,*) get_my_mpi_work_id(), "egdes halo_level:", patch%edges%halo_level
    !     write(0,*) get_my_mpi_work_id(), "egdes owner:", patch%edges%owner_local
    !     IF (get_my_mpi_work_id() == 0) CALL work_mpi_barrier()
    
  END SUBROUTINE fill_grid_subsets
  !-----------------------------------------------------------------------------
  
  !-----------------------------------------------------------------------------
  !>
  !! Read the grid's subsets
  SUBROUTINE read_grid_subsets(ncid, patch)
    INTEGER, INTENT(in) :: ncid
    TYPE(t_patch), INTENT(inout) :: patch
    
    !--------------------------------------------------------------------------------
    ! aliasing the halo_level to decomp_domain
    patch%cells%halo_level => patch%cells%decomp_domain
    patch%edges%halo_level => patch%edges%decomp_domain
    patch%verts%halo_level => patch%verts%decomp_domain
    !--------------------------------------------------------------------------------
    ! make sure the levels are correct when running sequentially
    IF (my_process_is_mpi_seq()) THEN
      patch%cells%halo_level(:,:) = 0
      patch%cells%halo_level(patch%npromz_c + 1 :nproma, patch%nblks_c) = -1
      patch%edges%halo_level(:,:) = 0
      patch%edges%halo_level(patch%npromz_e + 1 :nproma, patch%nblks_e) = -1
      patch%verts%halo_level(:,:) = 0
      patch%verts%halo_level(patch%npromz_v + 1 :nproma, patch%nblks_v) = -1
    ENDIF

    !--------------------------------------------------------------------------------
    CALL fill_grid_subset_names(patch)
    !--------------------------------------------------------------------------------
    ! fill cell subsets
    CALL read_subset(ncid, patch%cells%all, patch)
    CALL read_subset(ncid, patch%cells%owned, patch)    
    CALL read_subset(ncid, patch%cells%in_domain, patch)    
    CALL read_subset(ncid, patch%cells%not_owned, patch)    
    CALL read_subset(ncid, patch%cells%not_in_domain,  patch)
    CALL read_subset(ncid, patch%cells%one_edge_in_domain, patch)
    
    ! fill edge subsets
    CALL read_subset(ncid, patch%edges%all, patch)
    CALL read_subset(ncid, patch%edges%owned, patch)
    CALL read_subset(ncid, patch%edges%in_domain, patch)
    CALL read_subset(ncid, patch%edges%not_owned, patch)
    CALL read_subset(ncid, patch%edges%not_in_domain, patch)
    
    ! fill vertex subsets
    CALL read_subset(ncid, patch%verts%all,  patch)
    CALL read_subset(ncid, patch%verts%owned, patch)
    CALL read_subset(ncid, patch%verts%in_domain, patch)
    CALL read_subset(ncid, patch%verts%not_owned, patch)
    CALL read_subset(ncid, patch%verts%not_in_domain, patch)
    
  END SUBROUTINE read_grid_subsets
  !-----------------------------------------------------------------------------
  
  !-----------------------------------------------------------------------------
  !>
  !! Write the grid's subsets
  SUBROUTINE write_grid_subsets(ncid, patch)
    INTEGER, INTENT(in) :: ncid
    TYPE(t_patch), INTENT(inout) :: patch
    
    !--------------------------------------------------------------------------------
    ! write cell subsets
    CALL write_subset(ncid, patch%cells%all)
    CALL write_subset(ncid, patch%cells%owned)
    CALL write_subset(ncid, patch%cells%in_domain)
    CALL write_subset(ncid, patch%cells%not_owned)
    CALL write_subset(ncid, patch%cells%not_in_domain)
    CALL write_subset(ncid, patch%cells%one_edge_in_domain)
    
    ! write edge subsets
    CALL write_subset(ncid, patch%edges%all)
    CALL write_subset(ncid, patch%edges%owned)
    CALL write_subset(ncid, patch%edges%in_domain)
    CALL write_subset(ncid, patch%edges%not_owned)
    CALL write_subset(ncid, patch%edges%not_in_domain)
    
    ! write vertex subsets
    CALL write_subset(ncid, patch%verts%all)
    CALL write_subset(ncid, patch%verts%owned)
    CALL write_subset(ncid, patch%verts%in_domain)
    CALL write_subset(ncid, patch%verts%not_owned)
    CALL write_subset(ncid, patch%verts%not_in_domain)
    
  END SUBROUTINE write_grid_subsets
  !-----------------------------------------------------------------------------
    
  
  !-----------------------------------------------------------------------------
  !>
  !! Fills the grid's subset names
  SUBROUTINE fill_grid_subset_names(patch)
    TYPE(t_patch), INTENT(inout) :: patch
       
    patch%cells%all%name                = "cells_all"
    patch%cells%owned%name              =  "cells_owned"
    patch%cells%in_domain%name          =  "cells_in_domain"
    patch%cells%not_owned%name          = "cells_not_owned"
    patch%cells%not_in_domain%name      = "cells_not_in_domain"
    patch%cells%one_edge_in_domain%name =  "cells_one_edge_in_domain"
    
    patch%edges%all%name             = "edges_all"
    patch%edges%owned%name           =  "edges_owned"
    patch%edges%in_domain%name       =  "edges_in_domain"
    patch%edges%not_owned%name       =  "edges_not_owned"
    patch%edges%not_in_domain%name   =  "edges_not_in_domain"
    
    patch%verts%all%name             = "verts_all"
    patch%verts%owned%name           = "verts_owned"
    patch%verts%in_domain%name       = "verts_in_domain"
    patch%verts%not_owned%name       = "verts_not_owned"    
    patch%verts%not_in_domain%name   = "verts_not_in_domain"
            
  END SUBROUTINE fill_grid_subset_names
  !-----------------------------------------------------------------------------
  
END MODULE mo_model_domimp_setup
