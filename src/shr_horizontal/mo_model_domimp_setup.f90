!>
!!               The module <i>mo_model_import_domain</i>.
!!
!!               The module <i>mo_model_import_domain</i>
!! provides functionality to import information about the models computational
!! domain. This information is read from several files that were generated by
!! the patch generator programm. The data types describing the model domain are
!! contained in <i>mo_domain_model</i>.
!!
!! @par Revision History
!! Initial version  by: Peter Korn,  MPI-M, Hamburg, June 2005
!! Modification by Thomas Heinze (2006-02-21):
!! - renamed m_modules to mo_modules
!! Modification by Thomas Heinze (2006-09-20):
!! - added routine grid_and_patch_diagnosis
!! Modification by Pilar Ripodas, DWD, (2007-01-31)
!! - addapted to the new TYPE grid_edges (tangent_orientation added)
!! Modification by Peter Korn,  MPI-M, (2006-12)
!! - implementation of topography and boundary treatment, i.e.
!!   initialization of the grid & patch components that carry
!!   information about topography and the lateral boundaries of
!!   the domain; this is not related to patch boundaries.
!!   topography can either be computed by analytical l,eans or
!!   by reading from database files.
!! Modification by Hui Wan, MPI-M, (2007-02-23)
!! - Subroutine <i>init_import</i> was changed to <i>setup_grid</i>.
!!   Namelist hierarchy_ini was renamed to <i>grid_ctl</i>, moved
!!   from <i>mo_io_utilities</i> to this module and now read from
!!   an external file in subroutine <i>setup_grid</i>.
!! - Some changes in <i>init_ocean_patch_component</i> after
!!   discussion with Peter.
!! - Calculation of the min. primal edge length was added to
!!   <i>import_patches</i>. However, shouldn't it be an array with
!!   one element for each patch, rather than a scalar?
!! Modification by P. Ripodas, DWD, (2007-03-14):
!! - Now the output of "import_patches" is the min_dual_edge_lenght
!!   instead of the min_primal_edge_lenght. It will be used to set
!!   the horizontal diffusion parameter. Now it is done as it was
!!   in the prototype.
!! Modification by Almut Gassmann, MPI-M (2007-04)
!! - removed loptimize to make compatible with new grid generator
!! - removed itoa for good programming style
!! - reorganized patch input to be compatible with the new patch generator
!! - cleaning up "destruct_patches"
!! Modification by Almut Gassmann, MPI-M (2007-04-13)
!! - remove grid type and perform related adaptations
!!   (grid information comes now inside a patch)
!! - changed subroutine name form setup_grid to setup_files
!! Modified by Hui Wan, MPI-M, (2008-04-04)
!!  - topography_file_dir renamed topo_file_dir
!!  - for the hydro_atmos, control variable testtype renamed ctest_name.
!! Modified by Almut Gassmann, MPI-M, (2008-04-23)
!!  - itopo distinguishes now shallow water (itopo=1) orography function
!!    from hydro_atmos orography function (itopo=2)
!! Modification by Jochen Foerstner, DWD, (2008-07-16)
!!  - new fields in the derived type for the edges:
!!    grid_edges%primal_cart_normal (Cartesian normal to edge),
!!    grid_edges%quad_idx, grid_edges%quad_area and grid_edges%quad_orientation
!!    (indices of edges and area of the quadrilateral formed by two adjacent cells)
!!    up to now these new fields are initialized in the new routines
!!    calculate_primal_cart_normal and init_quad_twoadjcells
!!    rather than read from a grid/patch file.
!! Modification by Almut Gassmann, MPI-M, (2008-09-21)
!!  - remove reference to mask and height files, they are never used
!!  - use patch%geometry_info%cell_type to distinguish cells as triangles or hexagons
!! Modification by Almut Gassmann, MPI-M (2008-10-30)
!!  - add subroutine init_coriolis to initialize Coriolis parameter
!! Modification by Constantin Junk, MPI-M (2011-04-05)
!! - ...
!! Modification by Daniel Reinert, DWD (2012-04-11)
!!  - new routine which initializes the butterfly data structure
!!
!! @par Copyright and License
!!
!! This code is subject to the DWD and MPI-M-Software-License-Agreement in
!! its most recent form.
!! Please see the file LICENSE in the root of the source tree for this code.
!! Where software is supplied by third parties, it is indicated in the
!! headers of the routines.
!!
!!

!----------------------------
#include "omp_definitions.inc"
!----------------------------

MODULE mo_model_domimp_setup
  !-------------------------------------------------------------------------
  USE mo_kind,               ONLY: wp
  USE mo_exception,          ONLY: finish, warning
  USE mo_model_domain,       ONLY: t_patch
  USE mo_parallel_config,    ONLY: nproma
  USE mo_grid_config,        ONLY: corio_lat, grid_angular_velocity, use_dummy_cell_closure, &
    &                              grid_sphere_radius
  USE mo_sync,               ONLY: sync_c, sync_e, sync_patch_array, sync_idx
  USE mo_grid_subset,        ONLY: fill_subset,read_subset, write_subset
  USE mo_run_config,         ONLY: msg_level

  USE mo_loopindices
  USE mo_impl_constants
  USE mo_math_types
  USE mo_math_utilities
  USE mo_grid_geometry_info, ONLY: planar_torus_geometry, planar_channel_geometry
  USE mo_master_control,     ONLY: my_process_is_oceanic
  USE mo_fortran_tools,      ONLY: init

  IMPLICIT NONE

  PRIVATE

  CHARACTER(LEN=*), PARAMETER :: modname = 'mo_model_domimp_setup'

  PUBLIC :: reshape_int
  PUBLIC :: reshape_real
  PUBLIC :: init_quad_twoadjcells
  PUBLIC :: init_coriolis
  PUBLIC :: init_centrifugal
  PUBLIC :: set_verts_phys_id
  PUBLIC :: init_butterfly_idx

  PUBLIC :: fill_grid_subsets, fill_grid_subset_names, read_grid_subsets, write_grid_subsets


  !-------------------------------------------------------------------------

CONTAINS


  !-------------------------------------------------------------------------
  !>
  !! This routine initializes the data for the quadrilateral cells.
  !!
  !! This routine initializes the data for the quadrilateral cells
  !! formed by the two adjacent cells of an edge.
  !! The four edge indices of this qudrilateral and its area are stored
  !! in the derived type for the edges.
  !! This information should be provided by the grid generator and
  !! read in the routine read_patch.
  !!
  !! @par Revision History
  !! Initial release by Jochen Foerstner (2008-07-16)
  !! Modification by Guenther Zaengl (2009-05-08): vectorization
  !! Also suitable for hexagons: Almut Gassmann (2009-10-01)
  !!
  SUBROUTINE init_quad_twoadjcells( patch )
    !
    TYPE(t_patch), TARGET, INTENT(inout) :: patch  ! patch on a specific level

    INTEGER :: nblks_e
    INTEGER :: je, jb                   ! loop index
    INTEGER :: iie, iie1(nproma), iie2(nproma), ierror(nproma)
    INTEGER :: ilc1, ibc1, ilc2, ibc2                   ! cell indices
    INTEGER :: ile1, ibe1, ile2, ibe2, ile3, ibe3       ! edge indices
    INTEGER :: ilv1, ibv1, ilv2, ibv2, ilev, ibev, jjev ! vertex indices
    INTEGER :: i_startblk, i_startidx, i_endidx


    !-----------------------------------------------------------------------
    ! values for the blocking
    nblks_e  = patch%nblks_e

    ! Quad cells cannot be computed along the lateral edges of a nested domain
    i_startblk = patch%edges%start_blk(2,1)

!$OMP PARALLEL

    ! Initialize array elements along nest boundaries with zero
    IF (patch%id > 1) THEN
      CALL init(patch%edges%quad_area(:,1:i_startblk))
      CALL init(patch%edges%quad_idx(:,1:i_startblk,1:4))
      CALL init(patch%edges%quad_blk(:,1:i_startblk,1:4))
      CALL init(patch%edges%quad_orientation(:,1:i_startblk,1:4))
!$OMP BARRIER
    ENDIF

!$OMP DO PRIVATE(jb,i_startidx,i_endidx,je,iie,ilc1,ibc1,ilc2,ibc2,ile1,ibe1,ile2,ibe2,&
!$OMP            ile3,ibe3,iie1,iie2,ierror,ilv1,ibv1,ilv2,ibv2,ilev,ibev,&
!$OMP jjev) ICON_OMP_DEFAULT_SCHEDULE
    DO jb = i_startblk, nblks_e

      CALL get_indices_e(patch, jb, i_startblk, nblks_e, &
        & i_startidx, i_endidx, 2)

      IF (patch%geometry_info%cell_type == 3) THEN ! vectorized code for triangular grid

        ierror = 0

        DO je = i_startidx, i_endidx

          IF(.NOT.patch%edges%decomp_info%owner_mask(je,jb)) CYCLE
          !
          ! get global indices of the edges of the two neighboring cells
          !
          ilc1 = patch%edges%cell_idx(je,jb,1)
          ibc1 = patch%edges%cell_blk(je,jb,1)
          ilc2 = patch%edges%cell_idx(je,jb,2)
          ibc2 = patch%edges%cell_blk(je,jb,2)

          ! sum area of the two adjacent cells
          patch%edges%quad_area(je,jb) = &
            & patch%cells%area(ilc1,ibc1) + patch%cells%area(ilc2,ibc2)

          ! Indices of edges adjacent to cell 1
          ile1 = patch%cells%edge_idx(ilc1,ibc1,1)
          ibe1 = patch%cells%edge_blk(ilc1,ibc1,1)
          ile2 = patch%cells%edge_idx(ilc1,ibc1,2)
          ibe2 = patch%cells%edge_blk(ilc1,ibc1,2)
          ile3 = patch%cells%edge_idx(ilc1,ibc1,3)
          ibe3 = patch%cells%edge_blk(ilc1,ibc1,3)

          IF (je == ile1 .AND. jb == ibe1) THEN
            iie1(je) = 2
            iie2(je) = 3
          ELSE IF (je == ile2 .AND. jb == ibe2) THEN
            iie1(je) = 3
            iie2(je) = 1
          ELSE IF (je == ile3 .AND. jb == ibe3) THEN
            iie1(je) = 1
            iie2(je) = 2
          ELSE
            ierror(je) = ierror(je) + 1
          ENDIF

          patch%edges%quad_idx(je,jb,1) = patch%cells%edge_idx(ilc1,ibc1,iie1(je))
          patch%edges%quad_blk(je,jb,1) = patch%cells%edge_blk(ilc1,ibc1,iie1(je))
          patch%edges%quad_orientation(je,jb,1) =  &
            & patch%cells%edge_orientation(ilc1,ibc1,iie1(je))

          patch%edges%quad_idx(je,jb,2) = patch%cells%edge_idx(ilc1,ibc1,iie2(je))
          patch%edges%quad_blk(je,jb,2) = patch%cells%edge_blk(ilc1,ibc1,iie2(je))
          patch%edges%quad_orientation(je,jb,2) =  &
            & patch%cells%edge_orientation(ilc1,ibc1,iie2(je))

          ! Indices of edges adjacent to cell 2
          ile1 = patch%cells%edge_idx(ilc2,ibc2,1)
          ibe1 = patch%cells%edge_blk(ilc2,ibc2,1)
          ile2 = patch%cells%edge_idx(ilc2,ibc2,2)
          ibe2 = patch%cells%edge_blk(ilc2,ibc2,2)
          ile3 = patch%cells%edge_idx(ilc2,ibc2,3)
          ibe3 = patch%cells%edge_blk(ilc2,ibc2,3)

          IF (je == ile1 .AND. jb == ibe1) THEN
            iie1(je) = 2
            iie2(je) = 3
          ELSE IF (je == ile2 .AND. jb == ibe2) THEN
            iie1(je) = 3
            iie2(je) = 1
          ELSE IF (je == ile3 .AND. jb == ibe3) THEN
            iie1(je) = 1
            iie2(je) = 2
          ELSE
            ierror(je) = ierror(je) + 1
          ENDIF

          patch%edges%quad_idx(je,jb,3) = patch%cells%edge_idx(ilc2,ibc2,iie1(je))
          patch%edges%quad_blk(je,jb,3) = patch%cells%edge_blk(ilc2,ibc2,iie1(je))
          patch%edges%quad_orientation(je,jb,3) =  &
            & patch%cells%edge_orientation(ilc2,ibc2,iie1(je))

          patch%edges%quad_idx(je,jb,4) = patch%cells%edge_idx(ilc2,ibc2,iie2(je))
          patch%edges%quad_blk(je,jb,4) = patch%cells%edge_blk(ilc2,ibc2,iie2(je))
          patch%edges%quad_orientation(je,jb,4) =  &
            & patch%cells%edge_orientation(ilc2,ibc2,iie2(je))

        ENDDO
        IF (MAXVAL(ierror) > 0) THEN
          WRITE(0,*) "Number of errors", SUM(ierror(1:nproma))
          CALL finish (modname//':init_quad_twoadjcells',  &
            & 'edge-cell index relationships are apparently incorrect')
        ENDIF

      ELSE ! patch%geometry_info%cell_type == 6

        DO je = i_startidx, i_endidx

          IF(.NOT.patch%edges%decomp_info%owner_mask(je,jb)) CYCLE

          iie = 0
          !
          ! get global indices of the edges of the two neighboring verts
          !
          ilv1 = patch%edges%vertex_idx(je,jb,1)
          ibv1 = patch%edges%vertex_blk(je,jb,1)
          ilv2 = patch%edges%vertex_idx(je,jb,2)
          ibv2 = patch%edges%vertex_blk(je,jb,2)

          ! sum area of the two adjacent verts
          patch%edges%quad_area(je,jb) = &
            & patch%verts%dual_area(ilv1,ibv1) + patch%verts%dual_area(ilv2,ibv2)

          DO jjev = 1, 3

            ilev = patch%verts%edge_idx(ilv1,ibv1,jjev)
            ibev = patch%verts%edge_blk(ilv1,ibv1,jjev)

            ! test if edge is not the current one, then store line and
            ! block indices and orientation of quad edge
            IF ( ilev /= je .OR. ibev /= jb ) THEN
              iie = iie+1
              patch%edges%quad_idx(je,jb,iie) = ilev
              patch%edges%quad_blk(je,jb,iie) = ibev
              patch%edges%quad_orientation(je,jb,iie) =  &
                & patch%verts%edge_orientation(ilv1,ibv1,jjev)
            ENDIF

          ENDDO

          DO jjev = 1, 3

            ilev = patch%verts%edge_idx(ilv2,ibv2,jjev)
            ibev = patch%verts%edge_blk(ilv2,ibv2,jjev)

            ! test if edge is not the current one, then store line and
            ! block indices and orientation of quad edge
            IF ( ilev /= je .OR. ibev /= jb ) THEN
              iie = iie+1
              patch%edges%quad_idx(je,jb,iie) = ilev
              patch%edges%quad_blk(je,jb,iie) = ibev
              patch%edges%quad_orientation(je,jb,iie) =  &
                & patch%verts%edge_orientation(ilv2,ibv2,jjev)
            ENDIF

          ENDDO

          IF (iie /= 4) THEN
            PRINT *, "ERROR ==>  iie = ", iie,  " /= 4 "
            CALL finish (modname//':init_quad_twoadjcells',  &
              & 'wrong number of edge indices for quad')
          ENDIF

        END DO ! end edge loop

      ENDIF ! patch%geometry_info%cell_type

    END DO
!$OMP END DO NOWAIT
!$OMP END PARALLEL

    CALL sync_patch_array(sync_e,patch,patch%edges%quad_area)
    DO iie = 1, 4
      CALL sync_patch_array(sync_e,patch,patch%edges%quad_orientation(:,:,iie))

      CALL sync_idx(sync_e,sync_e,patch,patch%edges%quad_idx(:,:,iie), &
        & patch%edges%quad_blk(:,:,iie), &
        & opt_remap=.FALSE.)
    ENDDO

  END SUBROUTINE init_quad_twoadjcells
  !-------------------------------------------------------------------------


  !-------------------------------------------------------------------------
  !>
  !! This routine initializes the butterfly index field.
  !!
  !! This routine initializes the butterfly index field, formed by the
  !! 4 cells sharing the 2 vertices which bound a given edge.
  !! The 4 cell indices are stored in the derived type for the edges.
  !!
  !!     -------------------------------
  !!       \          /\          /
  !!        \ (2,2)  /  \  (2,1) /             (edge_neighbor,cell)
  !!         \      /    \      /
  !!          \    /      \    /
  !!           \  /   2    \  /             ^  : edge normal
  !!            \/          \/             /|\
  !!       ----2-============-1---------    |
  !!            /\          /\
  !!           /  \   1    /  \
  !!          /    \      /    \
  !!         /      \    /      \
  !!        /  (1,2) \  /  (1,1) \
  !!       /          \/          \
  !!     -------------------------------
  !!
  !!
  !! Storage order:  butterfly_idx(nproma,nblks_c,edge_neighbor,cell)
  !!                 butterfly_blk(nproma,nblks_c,edge_neighbor,cell)
  !!
  !! The cells are subdivided into two classes: Cells which are
  !! neighbors of edge-neighbor 1 and cells which are neighbors of edge-
  !! neighbor 2. These cells are then numbered according to the number
  !! of the edge-vertex they share. (see ASCII-ART)
  !!
  !! @par Revision History
  !! Initial release by Daniel Reinert (2012-04-05)
  !!
  SUBROUTINE init_butterfly_idx( patch )
    !
    TYPE(t_patch), TARGET, INTENT(inout) :: patch  ! patch on a specific level

    INTEGER :: cnt               ! counter
    INTEGER :: nblks_e
    INTEGER :: rl_start
    INTEGER :: je, jen, jn, jv, jb                   ! loop index
    INTEGER :: iln, ibn                              ! cell indices (neighbors)
    INTEGER :: iie, ije
    INTEGER :: il_bf1(2), ib_bf1(2)
    INTEGER :: il_bf2(2), ib_bf2(2)
    INTEGER :: ilc1, ibc1, ilc2, ibc2                ! cell indices
    INTEGER :: ilv1, ibv1, ilv2, ibv2, ilv, ibv      ! vertex indices
    INTEGER :: i_startblk, i_startidx, i_endidx


    !-----------------------------------------------------------------------

    ! values for the blocking
    nblks_e  = patch%nblks_e

    rl_start = 3
    i_startblk = patch%edges%start_blk(rl_start,1)

!$OMP PARALLEL

    ! Initialize array elements along nest boundaries with zero
    IF (patch%id > 1) THEN
      CALL init(patch%edges%butterfly_idx(:,1:i_startblk,:,:))
      CALL init(patch%edges%butterfly_blk(:,1:i_startblk,:,:))
!$OMP BARRIER
    ENDIF

!$OMP DO PRIVATE(jb,je,i_startidx,i_endidx,ilc1,ilc2,ibc1,ibc2, &
!$OMP            cnt,jen,iln,ibn,il_bf1,ib_bf1,il_bf2,ib_bf2,   &
!$OMP            ilv1,ilv2,ibv1,ibv2,jn,jv,ilv,ibv)
    DO jb = i_startblk, nblks_e

      CALL get_indices_e(patch, jb, i_startblk, nblks_e, &
        & i_startidx, i_endidx, rl_start)


      DO je = i_startidx, i_endidx

        IF(.NOT.patch%edges%decomp_info%owner_mask(je,jb)) CYCLE

        !
        ! get global indices of the two neighboring cells
        !
        ilc1 = patch%edges%cell_idx(je,jb,1)
        ibc1 = patch%edges%cell_blk(je,jb,1)
        ilc2 = patch%edges%cell_idx(je,jb,2)
        ibc2 = patch%edges%cell_blk(je,jb,2)


        ! get global indices of the two cells neighboring
        ! each edge-neighbor, while excluding the other
        ! edge neighbor.
        !
        ! neighbor 1 (ilc1,ibc1)
        cnt = 0
        DO jen = 1,3
          iln= patch%cells%neighbor_idx(ilc1,ibc1,jen)
          ibn= patch%cells%neighbor_blk(ilc1,ibc1,jen)
          IF ((iln == ilc2) .AND. (ibn == ibc2)) CYCLE
          cnt = cnt + 1
          il_bf1(cnt) = iln
          ib_bf1(cnt) = ibn
        ENDDO

        ! neighbor 2 (ilc2,ibc2)
        cnt = 0
        DO jen = 1,3
          iln= patch%cells%neighbor_idx(ilc2,ibc2,jen)
          ibn= patch%cells%neighbor_blk(ilc2,ibc2,jen)
          IF ((iln == ilc1) .AND. (ibn == ibc1)) CYCLE
          cnt = cnt + 1
          il_bf2(cnt) = iln
          ib_bf2(cnt) = ibn
        ENDDO

        ! Now, the global indices of the 4 cells are known, and
        ! they are already distinguished with respect to the edge
        ! neighbor. However, we do not know yet, whether they share
        ! vertex 1 or 2.

        ! Indices of vertices bounding edge je
        !
        ilv1= patch%edges%vertex_idx(je,jb,1)
        ibv1= patch%edges%vertex_blk(je,jb,1)
        ilv2= patch%edges%vertex_idx(je,jb,2)
        ibv2= patch%edges%vertex_blk(je,jb,2)


        ! check neighbors of neighbor 1, whether they share
        ! vertex 1 or 2
        DO jn= 1, 2   ! loop over 2 neighbors of neighbor 1
          !
          ! get vertex indices of neighbor jn
          DO jv=1,3
            ilv = patch%cells%vertex_idx(il_bf1(jn),ib_bf1(jn),jv)
            ibv = patch%cells%vertex_blk(il_bf1(jn),ib_bf1(jn),jv)

            IF ( ilv == ilv1 .AND. ibv == ibv1 ) THEN
              patch%edges%butterfly_idx(je,jb,1,1) = il_bf1(jn)
              patch%edges%butterfly_blk(je,jb,1,1) = ib_bf1(jn)
            ELSE IF ( ilv == ilv2 .AND. ibv == ibv2 ) THEN
              patch%edges%butterfly_idx(je,jb,1,2) = il_bf1(jn)
              patch%edges%butterfly_blk(je,jb,1,2) = ib_bf1(jn)
            ENDIF
          ENDDO
        ENDDO

        ! check neighbors of neighbor 2, whether they share
        ! vertex 1 or 2
        DO jn= 1, 2   ! loop over 2 neighbors of neighbor 2
          !
          ! get vertex indices of neighbor jn
          DO jv=1,3
            ilv = patch%cells%vertex_idx(il_bf2(jn),ib_bf2(jn),jv)
            ibv = patch%cells%vertex_blk(il_bf2(jn),ib_bf2(jn),jv)

            IF ( ilv == ilv1 .AND. ibv == ibv1 ) THEN
              patch%edges%butterfly_idx(je,jb,2,1) = il_bf2(jn)
              patch%edges%butterfly_blk(je,jb,2,1) = ib_bf2(jn)
            ELSE IF ( ilv == ilv2 .AND. ibv == ibv2 ) THEN
              patch%edges%butterfly_idx(je,jb,2,2) = il_bf2(jn)
              patch%edges%butterfly_blk(je,jb,2,2) = ib_bf2(jn)
            ENDIF
          ENDDO
        ENDDO

      END DO  ! je
    END DO  ! jb
!$OMP END DO
!$OMP END PARALLEL

    DO iie = 1, 2
      DO ije = 1, 2
        CALL sync_idx(sync_e,sync_c,patch,            &
          &  patch%edges%butterfly_idx(:,:,iie,ije),  &
          &  patch%edges%butterfly_blk(:,:,iie,ije),  &
          &  opt_remap=.FALSE.)
      ENDDO
    ENDDO


  END SUBROUTINE init_butterfly_idx
  !-------------------------------------------------------------------------

  !-------------------------------------------------------------------------
  !>
  !! Initializes the Coriolis components of the grid with analytical values.
  !!
  !! @par Revision History
  !! Developed by Almut Gassmann, MPI-M (2008-10-30).
  !! Modification by Daniel Reinert, DWD (2010-07-13),
  !! - Allocation of f_c, f_e, f_v has been moved to read_patch
  !!
  SUBROUTINE init_coriolis( lcorio, lplane, patch, opt_lnontrad )

    LOGICAL,INTENT(in) :: lcorio, lplane
    TYPE(t_patch), TARGET, INTENT(inout) :: patch ! patch on specific level
    LOGICAL, OPTIONAL, INTENT(in) :: opt_lnontrad

    INTEGER :: nlen,                         &
      & nblks_c,  nblks_e,  nblks_v,  &
      & npromz_c, npromz_e, npromz_v
    INTEGER :: jc, je, jv, jb

    LOGICAL :: is_plane
    REAL(wp) :: zlat

    REAL(wp) :: z_n_u, z_n_v, z_n_norm
    REAL(wp) :: z_t_u, z_t_v, z_t_norm
    REAL(wp) :: z_l
    REAL(wp), PARAMETER :: z_eps = 1.0E-25_wp

    !-----------------------------------------------------------------------

    ! values for the blocking
    nblks_c  = patch%nblks_c
    npromz_c = patch%npromz_c
    nblks_e  = patch%nblks_e
    npromz_e = patch%npromz_e
    nblks_v  = patch%nblks_v
    npromz_v = patch%npromz_v

    is_plane = lplane  &
      &    .OR.    (patch%geometry_info%geometry_type == planar_torus_geometry) &
      &    .OR.    (patch%geometry_info%geometry_type == planar_channel_geometry )

    IF (lcorio .AND. .NOT. is_plane) THEN

      DO jb = 1, nblks_c
        nlen = MERGE(nproma, npromz_c, jb /= nblks_c)
        DO jc = 1, nlen
          zlat = patch%cells%center(jc,jb)%lat
          patch%cells%f_c(jc,jb) = 2._wp * grid_angular_velocity * SIN(zlat)
        END DO
      END DO

      DO jb = 1, nblks_e
        nlen = MERGE(nproma, npromz_e, jb /= nblks_e)
        DO je = 1, nlen
          zlat = patch%edges%center(je,jb)%lat
          patch%edges%f_e(je,jb) = 2._wp * grid_angular_velocity * SIN(zlat)
        END DO
      END DO

      DO jb = 1, nblks_v
        nlen = MERGE(nproma, npromz_v, jb /= nblks_v)
        DO jv = 1, nlen
          zlat = patch%verts%vertex(jv,jb)%lat
          patch%verts%f_v(jv,jb) = 2._wp * grid_angular_velocity * SIN(zlat)
        END DO
      END DO

      IF (PRESENT(opt_lnontrad)) THEN
        ! compute components of horizontal Coriolis parameter (vector) 'fn' and 'ft'
        ! (default values: 'fn = 0' and 'ft = 0', see 'src/shr_horizontal/mo_alloc_patches'
        ! -> that is why only the case 'opt_lnontrad=.TRUE.' is computed explicitly below)
        ! NOTE: 'fn' and 'ft' are only available on edges!
        
        IF (opt_lnontrad) THEN
          ! non-traditional deep-atmosphere terms in components 
          ! of momentum equation (such as ft*w or vn/r) are active
          
          ! Given a triangular cell edge and the corresponding 
          ! normal unit vector N, pointing from the cell center on edge side 
          ! labeled 1 to the cell center on the other side labeled 2, 
          ! and the tangential unit vector T, pointing from the edge end 
          ! labeled vertex 1 to the edge end labeled vertex 2, 
          ! according to Hui Wan's PhD-thesis, p. 14 and/or Zaengl et al. 2015, 
          ! the following assumptions are made for the computation of 
          ! the horizontal Coriolis parameters:  
          ! - the horizontal wind vector u is measured with N and T in ICON: 
          !                  u = vn*N + vt*T 
          ! - from our attempts to track down in which model variables N and T 
          !   are actually stored, we guess the following:
          !   N -> patch%edges%primal_normal
          !   T -> patch%edges%dual_normal
          ! - the vertical unit vector Z (=e_z) points allways in the outward
          !   direction of the Earth sphere
          ! - IMPORTANT: T, N, Z form a right-handed coordinate system 
          !   in that order (not N, T, Z !)
          ! Next, having  2*Omega = fn*N + ft*T + fz*Z 
          ! and  v = vn*N + vt*T + w*Z, the Coriolis acceleration reads: 
          ! 2*Omegaxv = (fz*vt - ft*w)*N 
          !           + (fn*w  - fz*vn)*T 
          !           + (ft*vn - fn*vt)*Z 
          
          DO jb = 1, nblks_e
            nlen = MERGE(nproma, npromz_e, jb /= nblks_e)
            DO je = 1, nlen             
              ! zonal (u -> v1) and meridional (v -> v2) components of primal normal 
              ! (normal vector at edge, it seems that 
              ! 'primal_normal' is defined in 'src/shr_horizontal/mo_model_domain' 
              ! and read from file in 'src/shr_horizontal/mo_model_domimp_patches/
              ! read_remaining_patch')
              z_n_u = patch%edges%primal_normal(je,jb)%v1
              z_n_v = patch%edges%primal_normal(je,jb)%v2
              
              ! normalize vector N (just to make sure)
              z_n_norm = SQRT( z_n_u**2 + z_n_v**2 )
              z_n_u = z_n_u / MAX(z_eps, z_n_norm)
              z_n_v = z_n_v / MAX(z_eps, z_n_norm)

              ! the same procedure for the 'dual_normal' T
              z_t_u = patch%edges%dual_normal(je,jb)%v1
              z_t_v = patch%edges%dual_normal(je,jb)%v2
              z_t_norm = SQRT( z_t_u**2 + z_t_v**2 ) 
              z_t_u = z_t_u / MAX(z_eps, z_t_norm)
              z_t_v = z_t_v / MAX(z_eps, z_t_norm)              
                            
              ! latitude of edge point
              zlat = patch%edges%center(je,jb)%lat
              
              ! compute horizontal Coriolis parameter
              ! ('grid_angular_velocity' is defined and assigned in 
              ! 'src/configure_model/mo_grid_config')
              z_l = 2._wp * grid_angular_velocity * COS(zlat)
              
              ! compute normal and tangential components of horizontal 
              ! Coriolis parameter vector
              patch%edges%fn_e(je,jb) = z_l * z_n_v
              patch%edges%ft_e(je,jb) = z_l * z_t_v         
            END DO  !je = 1, nlen
          END DO  !jb = 1, nblks_e
          
        ENDIF  !IF (opt_lnontrad)
      
      ENDIF  !IF (PRESENT(opt_lnontrad))

    ELSEIF (lcorio .AND. is_plane) THEN

      patch%cells%f_c(:,:) = 2._wp*grid_angular_velocity*SIN(corio_lat)
      patch%edges%f_e(:,:) = 2._wp*grid_angular_velocity*SIN(corio_lat)
      patch%verts%f_v(:,:) = 2._wp*grid_angular_velocity*SIN(corio_lat)

      ! (deep atmosphere: no non-traditional Coriolis acceleration in this case, 
      ! should be coverd by the default settings: fn_e = ft_e = 0)      

    ELSE
      grid_angular_velocity = 0.0_wp
      patch%cells%f_c(:,:) = 0.0_wp
      patch%edges%f_e(:,:) = 0.0_wp
      patch%verts%f_v(:,:) = 0.0_wp

      ! (deep atmosphere: no non-traditional Coriolis acceleration in this case, 
      ! should be coverd by the default settings: fn_e = ft_e = 0)     

    ENDIF

  END SUBROUTINE init_coriolis
  !-------------------------------------------------------------------------


  !-------------------------------------------------------------------------
  !>
  !! Initializes the components of the centrifugal acceleration
  !! with analytical values (in large parts a copy of 'init_coriolis').
  !!
  !! @par Revision History
  !! Initial revision by Sebastian Borchert, DWD (2017-06-29).
  !!
  SUBROUTINE init_centrifugal( lcentrifugal,  & 
    &                          lplane,        & 
    &                          patch          )

    ! in/out-variables
    LOGICAL,               INTENT(IN)    :: lcentrifugal
    LOGICAL,               INTENT(IN)    :: lplane
    TYPE(t_patch), TARGET, INTENT(INOUT) :: patch

    ! local variables
    REAL(wp) :: zlat
    REAL(wp) :: z_n_u, z_n_v, z_n_norm

    INTEGER :: nlen
    INTEGER :: nblks_c, nblks_e
    INTEGER :: npromz_c, npromz_e
    INTEGER :: jc, je, jb

    LOGICAL :: is_plane

    REAL(wp), PARAMETER :: z_eps = 1.0E-25_wp
    
    !-----------------------------------------------------------------------

    ! values for the blocking
    nblks_c  = patch%nblks_c
    npromz_c = patch%npromz_c
    nblks_e  = patch%nblks_e
    npromz_e = patch%npromz_e

    is_plane = lplane                                                            &
      &    .OR.    (patch%geometry_info%geometry_type == planar_torus_geometry)  &
      &    .OR.    (patch%geometry_info%geometry_type == planar_channel_geometry )

    IF (lcentrifugal .AND. (.NOT. is_plane)) THEN

      ! vertical component of centrifugal acceleration (only on cell centers): 
      !
      ! dw/dt = ... + ( Omega * cos(phi) )^2 * a, 
      ! with Omega = angular velocity of Earth, phi = latitude, 
      ! a = radius of Earth (possible deep-atmosphere modification a -> a+z 
      ! takes place in dynamical core)
      ! 
      DO jb = 1, nblks_c
        nlen = MERGE(nproma, npromz_c, jb /= nblks_c)
        DO jc = 1, nlen
          zlat = patch%cells%center(jc,jb)%lat
          patch%cells%cz_c(jc,jb) = ( grid_angular_velocity * COS(zlat) )**2 & 
            &                       * grid_sphere_radius
        END DO  !jc
      END DO  !jb

      ! horizontal (normal) component of centrifugal acceleration (only on edges): 
      ! 
      ! dvn/dt = ... - Omega^2 * sin(phi) * cos(phi) * a * e_phi.e_n, 
      ! with e_phi.e_n denoting the scalar product of the meridional and 
      ! edge-normal unit vectors
      !
      DO jb = 1, nblks_e
        nlen = MERGE(nproma, npromz_e, jb /= nblks_e)
        DO je = 1, nlen
          ! zonal (u -> v1) and meridional (v -> v2) components of primal normal 
          ! (normal vector at edge, it seems that 
          ! 'primal_normal' is defined in 'src/shr_horizontal/mo_model_domain' 
          ! and read from file in 'src/shr_horizontal/mo_model_domimp_patches/
          ! read_remaining_patch')
          z_n_u = patch%edges%primal_normal(je,jb)%v1
          z_n_v = patch%edges%primal_normal(je,jb)%v2
          
          ! normalize vector e_n (just to make sure)
          z_n_norm = SQRT( z_n_u**2 + z_n_v**2 )
          z_n_v = z_n_v / MAX(z_eps, z_n_norm)

          zlat = patch%edges%center(je,jb)%lat
          patch%edges%cn_e(je,jb) = -grid_angular_velocity**2 * SIN(zlat) * COS(zlat) & 
            &                       * grid_sphere_radius * z_n_v
        END DO  !je
      END DO  !jb    

    ELSEIF (lcentrifugal .AND. is_plane) THEN

      ! currently centrifugal acceleration is unprovided for this cases 
      ! (default values for 'cz_c' and 'cn_e' should be zero, see 'src/shr_horizontal/mo_alloc_patches')

    ELSE

      ! currently centrifugal acceleration is unprovided for this cases 
      ! (default values for 'cz_c' and 'cn_e' should be zero, see 'src/shr_horizontal/mo_alloc_patches')
      
    ENDIF

  END SUBROUTINE init_centrifugal
  !-------------------------------------------------------------------------


  !-------------------------------------------------------------------------
  !>
  !! Sets phys_id for verts since this is not read from input
  !!
  !! @par Revision History
  !! Developed by Rainer Johanni (2010-12-05).
  !!
  SUBROUTINE set_verts_phys_id( patch )

    TYPE(t_patch), INTENT(inout) :: patch ! patch on specific level

    INTEGER :: nlen, jb, jv, ji, ilc, ibc

    !-----------------------------------------------------------------------

    IF (patch%geometry_info%cell_type == 3) THEN

      DO jb = 1, patch%nblks_v
        IF (jb /= patch%nblks_v) THEN
          nlen = nproma
        ELSE
          nlen = patch%npromz_v
        ENDIF
        DO jv = 1, nlen
          ! Just go over all neighboring cells of a vertex and take the phys_id
          ! of the first valid neighbor cell
          DO ji = 1, 6
            ilc = patch%verts%cell_idx(jv,jb,ji)
            ibc = patch%verts%cell_blk(jv,jb,ji)
            IF(ilc > 0) THEN
              patch%verts%phys_id(jv,jb) = patch%cells%phys_id(ilc, ibc)
              EXIT
            ENDIF
          END DO
        END DO
      END DO

    ELSE

      ! There is no distinction between physical and logical patches for hex grids
      patch%verts%phys_id(:,:) = patch%id

    ENDIF

  END SUBROUTINE set_verts_phys_id
  !-------------------------------------------------------------------------

  !-------------------------------------------------------------------------
  !>
  !!               reshape an integer array for the blocking.
  !!
  !! @par Revision History
  !! Developed  by  Jochen Foerstner, DWD (2008-10-21)
  !!
  SUBROUTINE reshape_int( p_int_array_in, nblks, npromz,  &
    & p_reshaped_int_array_out )


    ! input array
    INTEGER, INTENT(in) :: p_int_array_in(:)
    ! number of blocks
    INTEGER, INTENT(in) :: nblks
    ! chunk length
    INTEGER, INTENT(in) :: npromz

    ! output array
    INTEGER, INTENT(inout) :: p_reshaped_int_array_out(:,:)

    INTEGER :: nlen
    INTEGER :: jl, jb
    INTEGER :: il

    !-----------------------------------------------------------------------

    DO jb = 1, nblks

      IF (jb /= nblks) THEN
        nlen = nproma
      ELSE
        nlen = npromz
        DO jl = npromz+1, nproma
          p_reshaped_int_array_out(jl,nblks) = 0
        END DO
      END IF

      DO jl = 1, nlen
        il = jl + (jb-1)*nproma
        p_reshaped_int_array_out(jl,jb) = p_int_array_in(il)
      END DO

    END DO

  END SUBROUTINE reshape_int
  !-------------------------------------------------------------------------

  !-------------------------------------------------------------------------
  !>
  !!               reshape a real array for the blocking.
  !!
  !!
  !! @par Revision History
  !! Developed  by  Jochen Foerstner, DWD (2008-10-21)
  !!
  SUBROUTINE reshape_real( p_real_array_in, nblks, npromz,  &
    & p_reshaped_real_array_out )

    ! input array
    REAL(wp), INTENT(in):: p_real_array_in(:)
    ! number of blocks
    INTEGER, INTENT(in) :: nblks
    ! chunk length
    INTEGER, INTENT(in) :: npromz

    ! output array
    REAL(wp), INTENT(inout) :: p_reshaped_real_array_out(:,:)

    INTEGER :: nlen
    INTEGER :: jl, jb
    INTEGER :: il

    !-----------------------------------------------------------------------

    DO jb = 1, nblks

      IF (jb /= nblks) THEN
        nlen = nproma
      ELSE
        nlen = npromz
        DO jl = npromz+1, nproma
          p_reshaped_real_array_out(jl,nblks) = 0._wp
        END DO
      END IF

      DO jl = 1, nlen
        il = jl + (jb-1)*nproma
        p_reshaped_real_array_out(jl,jb) = p_real_array_in(il)
      END DO

    END DO

  END SUBROUTINE reshape_real
  !-------------------------------------------------------------------------

  !-----------------------------------------------------------------------------
  !>
  !! Fills the grid's subsets
  SUBROUTINE fill_grid_subsets(patch)
    TYPE(t_patch), TARGET :: patch

    CHARACTER(LEN=*), PARAMETER :: method_name = modname//':fill_grid_subsets'

    !--------------------------------------------------------------------------------
    ! make sure the levels are correct when running sequentially

    ! calculate dummy cell block, index, if exists
    ! set level halo to -1
    IF (use_dummy_cell_closure) THEN
      patch%cells%dummy_cell_block = patch%alloc_cell_blocks
      IF (patch%npromz_c < nproma) THEN
        patch%cells%dummy_cell_index = patch%npromz_c + 1
      ELSE
        patch%cells%dummy_cell_index = 1 ! we have a whole dummy block, ie alloc_cell_blocks = nblks_c + 1
        IF (patch%alloc_cell_blocks <= patch%nblks_c) &
          & CALL finish(method_name, "alloc_cell_blocks <= nblks_c")
   !     patch%cells%decomp_info%halo_level(:,patch%alloc_cell_blocks) = -1
      ENDIF
    ELSE
      patch%cells%dummy_cell_block = 0
      patch%cells%dummy_cell_index = 0
    ENDIF

    !--------------------------------------------------------------------------------
    CALL fill_grid_subset_names(patch)
    !--------------------------------------------------------------------------------
    ! fill cell subsets
    !    fill_subset(range subset,  halo levels, start level, end level)
 !   subset, patch, mask, start_mask, end_mask, subset_name
    CALL fill_subset(subset=patch%cells%all, patch=patch, &
      & mask=patch%cells%decomp_info%halo_level, start_mask=0, end_mask=halo_levels_ceiling, located=on_cells)
    patch%cells%all%is_in_domain   = .false.

    CALL fill_subset(subset=patch%cells%owned, patch=patch, &
      & mask=patch%cells%decomp_info%halo_level, start_mask=0, end_mask=0, located=on_cells)
    patch%cells%owned%is_in_domain = .true.

    CALL fill_subset(subset=patch%cells%in_domain, patch=patch, &
      & mask=patch%cells%decomp_info%halo_level, start_mask=0, end_mask=0, located=on_cells)
    patch%cells%in_domain%is_in_domain = .true.

    CALL fill_subset(subset=patch%cells%not_owned, patch=patch, &
      & mask=patch%cells%decomp_info%halo_level, start_mask=1, end_mask=halo_levels_ceiling, located=on_cells)
    patch%cells%not_owned%is_in_domain = .false.

    CALL fill_subset(subset=patch%cells%not_in_domain,  patch=patch, &
      & mask=patch%cells%decomp_info%halo_level, start_mask=2, end_mask=halo_levels_ceiling, located=on_cells)
    patch%cells%not_in_domain%is_in_domain = .false.

    CALL fill_subset(subset=patch%cells%one_edge_in_domain, patch=patch, &
      & mask=patch%cells%decomp_info%halo_level, start_mask=0, end_mask=1, located=on_cells)
    patch%cells%one_edge_in_domain%is_in_domain = .false.

!    IF (get_my_mpi_work_id() == 0) THEN
!      write(0,*) "patch%cells%decomp_info%halo_level:",patch%cells%decomp_info%halo_level
!    ENDIF

    ! fill edge subsets
    !    fill_subset(range subset,  halo levels, start level, end level)
    CALL fill_subset(subset=patch%edges%all, patch=patch, &
      & mask=patch%edges%decomp_info%halo_level, start_mask=0, end_mask=halo_levels_ceiling, located=on_edges)
    patch%edges%all%is_in_domain   = .false.

    CALL fill_subset(subset=patch%edges%owned, patch=patch, &
      & mask=patch%edges%decomp_info%halo_level, start_mask=0, end_mask=0, located=on_edges)
    patch%edges%owned%is_in_domain = .true.

    CALL fill_subset(subset=patch%edges%in_domain, patch=patch, &
      & mask=patch%edges%decomp_info%halo_level, start_mask=0, end_mask=1, located=on_edges)
    patch%edges%in_domain%is_in_domain   = .true.

    CALL fill_subset(subset=patch%edges%gradIsCalculable , patch=patch, &
      & mask=patch%edges%decomp_info%halo_level, start_mask=0, end_mask=2, located=on_edges)
    patch%edges%in_domain%is_in_domain   = .true.

    CALL fill_subset(subset=patch%edges%not_owned, patch=patch, &
      & mask=patch%edges%decomp_info%halo_level, start_mask=1, end_mask=halo_levels_ceiling, located=on_edges)
    patch%edges%not_owned%is_in_domain   = .false.

    CALL fill_subset(subset=patch%edges%not_in_domain, patch=patch, &
      & mask=patch%edges%decomp_info%halo_level, start_mask=2, end_mask=halo_levels_ceiling, located=on_edges)
    patch%edges%not_in_domain%is_in_domain   = .false.

    IF (patch%edges%owned%no_of_holes > 0 .AND. msg_level >= 20)  THEN
        CALL warning(method_name, "patch%edges%owned no_of_holes > 0")
    ENDIF
    IF (patch%edges%in_domain%no_of_holes > 0 .AND. msg_level >= 20)  THEN
        CALL warning(method_name, "patch%edges%in_domain no_of_holes > 0")
    ENDIF
!    IF (get_my_mpi_work_id() == 0) THEN
!      write(0,*) "patch%edges%decomp_info%halo_level:",patch%edges%decomp_info%halo_level
!      write(0,*) "=============================="
!    ENDIF


    ! fill vertex subsets
    !    fill_subset(range subset,  halo levels, start level, end level)
    CALL fill_subset(subset=patch%verts%all,  patch=patch, &
      & mask=patch%verts%decomp_info%halo_level, start_mask=0, end_mask=halo_levels_ceiling, located=on_vertices)
    patch%verts%all%is_in_domain   = .false.

    CALL fill_subset(subset=patch%verts%owned, patch=patch, &
      & mask=patch%verts%decomp_info%halo_level, start_mask=0, end_mask=0, located=on_vertices)
    patch%verts%owned%is_in_domain   = .true.

    CALL fill_subset(subset=patch%verts%in_domain, patch=patch, &
      & mask=patch%verts%decomp_info%halo_level, start_mask=0, end_mask=1, located=on_vertices)
    patch%verts%in_domain%is_in_domain   = .true.

    CALL fill_subset(subset=patch%verts%not_owned, patch=patch, &
      & mask=patch%verts%decomp_info%halo_level, start_mask=1, end_mask=halo_levels_ceiling, located=on_vertices)
    patch%verts%not_owned%is_in_domain   = .false.

    CALL fill_subset(subset=patch%verts%not_in_domain, patch=patch, &
      & mask=patch%verts%decomp_info%halo_level, start_mask=2, end_mask=halo_levels_ceiling, located=on_vertices)
    patch%verts%not_in_domain%is_in_domain   = .false.

    IF (my_process_is_oceanic()) THEN
      IF (patch%cells%in_domain%no_of_holes > 0) THEN
!      IF (get_my_mpi_work_id() == 0) THEN
!        write(0,*) "patch%cells%decomp_info%halo_level:",patch%cells%decomp_info%halo_level
!        write(0,*) "=============================="
!        write(0,*) "patch%cells%decomp_info%owner_mask:",patch%cells%decomp_info%owner_mask(:,:)
!        write(0,*) "=============================="
!        write(0,*) "patch%cells%owner:",patch%cells%decomp_info%owner_local(:)
!        write(0,*) "=============================="
!        write(0,*) "patch blocks:", patch%nblks_c, patch%alloc_cell_blocks
!        write(0,*) "subset blocks:", patch%cells%in_domain%start_block, patch%cells%in_domain%end_block
!        write(0,*) "=============================="
!       ENDIF
        CALL finish(method_name, "patch%cells%in_domain no_of_holes > 0")
      ENDIF

      IF (msg_level >= 5) THEN ! for nested atmospheric grids, this is normal
        IF (patch%cells%one_edge_in_domain%no_of_holes > 0)  THEN
!      IF (get_my_mpi_work_id() == 0) THEN
!        write(0,*) "patch%cells%decomp_info%halo_level:",patch%cells%decomp_info%halo_level
!        write(0,*) "=============================="
!        write(0,*) "patch%cells%decomp_info%owner_mask:",patch%cells%decomp_info%owner_mask(:,:)
!        write(0,*) "=============================="
!        write(0,*) "patch%cells%owner:",patch%cells%decomp_info%owner_local(:)
!        write(0,*) "=============================="
!        write(0,*) "patch blocks:", patch%nblks_c, patch%alloc_cell_blocks
!        write(0,*) "subset blocks:", patch%cells%one_edge_in_domain%start_block, patch%cells%in_domain%end_block
!        write(0,*) "=============================="
!        CALL finish(method_name, "patch%cells%one_edge_in_domain no_of_holes > 0")
!      ELSE
          CALL warning(method_name, "patch%cells%one_edge_in_domain no_of_holes > 0")
        ENDIF

        IF (patch%edges%owned%no_of_holes > 0)  THEN
           CALL warning(method_name, "patch%edges%owned%no_of_holes > 0")
        ENDIF
        IF (patch%edges%in_domain%no_of_holes > 0)  THEN
           CALL warning(method_name, "patch%edges%in_domain%no_of_holes > 0")
        ENDIF
        IF (patch%edges%gradIsCalculable%no_of_holes > 0)  THEN
           CALL warning(method_name, "patch%edges%gradIsCalculable%no_of_holes > 0")
        ENDIF

      ENDIF ! msg_level >= 5
    ENDIF ! my_process_is_oceanic
    ! write some info:
    !     IF (get_my_mpi_work_id() == 1) CALL work_mpi_barrier()
    !     write(0,*) get_my_mpi_work_id(), "egdes global_index:", patch%edges%decomp_info%glb_index
    !     write(0,*) get_my_mpi_work_id(), "egdes halo_level:", patch%edges%decomp_info%halo_level
    !     write(0,*) get_my_mpi_work_id(), "egdes owner:", patch%edges%decomp_info%owner_local
    !     IF (get_my_mpi_work_id() == 0) CALL work_mpi_barrier()

  END SUBROUTINE fill_grid_subsets
  !-----------------------------------------------------------------------------

  !-----------------------------------------------------------------------------
  !>
  !! Read the grid's subsets
  SUBROUTINE read_grid_subsets(ncid, patch)
    INTEGER, INTENT(in) :: ncid
    TYPE(t_patch), INTENT(inout) :: patch

    !--------------------------------------------------------------------------------
    CALL fill_grid_subset_names(patch)
    !--------------------------------------------------------------------------------
    ! fill cell subsets
    CALL read_subset(ncid, patch%cells%all, patch)
    CALL read_subset(ncid, patch%cells%owned, patch)
    CALL read_subset(ncid, patch%cells%in_domain, patch)
    CALL read_subset(ncid, patch%cells%not_owned, patch)
    CALL read_subset(ncid, patch%cells%not_in_domain,  patch)
    CALL read_subset(ncid, patch%cells%one_edge_in_domain, patch)

    ! fill edge subsets
    CALL read_subset(ncid, patch%edges%all, patch)
    CALL read_subset(ncid, patch%edges%owned, patch)
    CALL read_subset(ncid, patch%edges%in_domain, patch)
    CALL read_subset(ncid, patch%edges%gradIsCalculable, patch)
    CALL read_subset(ncid, patch%edges%not_owned, patch)
    CALL read_subset(ncid, patch%edges%not_in_domain, patch)

    ! fill vertex subsets
    CALL read_subset(ncid, patch%verts%all,  patch)
    CALL read_subset(ncid, patch%verts%owned, patch)
    CALL read_subset(ncid, patch%verts%in_domain, patch)
    CALL read_subset(ncid, patch%verts%not_owned, patch)
    CALL read_subset(ncid, patch%verts%not_in_domain, patch)

  END SUBROUTINE read_grid_subsets
  !-----------------------------------------------------------------------------

  !-----------------------------------------------------------------------------
  !>
  !! Write the grid's subsets
  SUBROUTINE write_grid_subsets(ncid, patch)
    INTEGER, INTENT(in) :: ncid
    TYPE(t_patch), INTENT(inout) :: patch

    !--------------------------------------------------------------------------------
    ! write cell subsets
    CALL write_subset(ncid, patch%cells%all)
    CALL write_subset(ncid, patch%cells%owned)
    CALL write_subset(ncid, patch%cells%in_domain)
    CALL write_subset(ncid, patch%cells%not_owned)
    CALL write_subset(ncid, patch%cells%not_in_domain)
    CALL write_subset(ncid, patch%cells%one_edge_in_domain)

    ! write edge subsets
    CALL write_subset(ncid, patch%edges%all)
    CALL write_subset(ncid, patch%edges%owned)
    CALL write_subset(ncid, patch%edges%in_domain)
    CALL write_subset(ncid, patch%edges%gradIsCalculable)
    CALL write_subset(ncid, patch%edges%not_owned)
    CALL write_subset(ncid, patch%edges%not_in_domain)

    ! write vertex subsets
    CALL write_subset(ncid, patch%verts%all)
    CALL write_subset(ncid, patch%verts%owned)
    CALL write_subset(ncid, patch%verts%in_domain)
    CALL write_subset(ncid, patch%verts%not_owned)
    CALL write_subset(ncid, patch%verts%not_in_domain)

  END SUBROUTINE write_grid_subsets
  !-----------------------------------------------------------------------------


  !-----------------------------------------------------------------------------
  !>
  !! Fills the grid's subset names
  SUBROUTINE fill_grid_subset_names(patch)
    TYPE(t_patch), INTENT(inout) :: patch

    patch%cells%all%name                = "cells_all"
    patch%cells%owned%name              = "cells_owned"
    patch%cells%in_domain%name          = "cells_in_domain"
    patch%cells%not_owned%name          = "cells_not_owned"
    patch%cells%not_in_domain%name      = "cells_not_in_domain"
    patch%cells%one_edge_in_domain%name = "cells_one_edge_in_domain"

    patch%edges%all%name             = "edges_all"
    patch%edges%owned%name           = "edges_owned"
    patch%edges%in_domain%name       = "edges_in_domain"
    patch%edges%gradIsCalculable%name  = "edges_gradIsCalculable"
    patch%edges%not_owned%name       = "edges_not_owned"
    patch%edges%not_in_domain%name   = "edges_not_in_domain"

    patch%verts%all%name             = "verts_all"
    patch%verts%owned%name           = "verts_owned"
    patch%verts%in_domain%name       = "verts_in_domain"
    patch%verts%not_owned%name       = "verts_not_owned"
    patch%verts%not_in_domain%name   = "verts_not_in_domain"

  END SUBROUTINE fill_grid_subset_names
  !-----------------------------------------------------------------------------

END MODULE mo_model_domimp_setup
